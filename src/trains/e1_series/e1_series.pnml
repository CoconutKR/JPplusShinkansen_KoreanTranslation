/* Begin E1 Series */

/* SUBTYPES */

// Pre 2003 subtype text
switch(FEAT_TRAINS, SELF, sw_e1_series_old_livery_subtype_text, cargo_subtype) {
    0: return string(LV_YEAR_DEP_LIVERY);
	1: return string(LV_E1_SERIES_ORIGINAL); 
       return CB_RESULT_NO_TEXT;
    }

// Post 2003 subtype text
switch(FEAT_TRAINS, SELF, sw_e1_series_new_livery_subtype_text, cargo_subtype) {
    0: return string(LV_YEAR_DEP_LIVERY);
	1: return string(LV_E1_SERIES_ORIGINAL);
    2: return string(LV_E1_SERIES_REFURB);
       return CB_RESULT_NO_TEXT;
    }

switch(FEAT_TRAINS, SELF, sw_e1_series_year_dep_subtype_text, current_year) {
    0..2003: sw_e1_series_old_livery_subtype_text;
    sw_e1_series_new_livery_subtype_text;
}

/* GRAPHICS */

/* Purchase Menu */
    spriteset (ss_e1_series_buy_original_livery_12,	        	    "src/trains/dummy/purchase.png") {template_purchase(0, 0)}
	spriteset (ss_e1_series_buy_original_livery_10,	        	    "src/trains/dummy/purchase.png") {template_purchase(0, 0)}
	spriteset (ss_e1_series_buy_refurb_livery_12,	        	    "src/trains/dummy/purchase.png") {template_purchase(0, 0)}
	spriteset (ss_e1_series_buy_refurb_livery_10,	        	    "src/trains/dummy/purchase.png") {template_purchase(0, 0)}

    alternative_sprites(ss_e1_series_buy_original_livery_12,        ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/buy_e1_original.png") {template_purchase(0, 0)}
	alternative_sprites(ss_e1_series_buy_original_livery_10,       	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/buy_e1_original.png") {template_purchase(0, 0)}
    alternative_sprites(ss_e1_series_buy_refurb_livery_12,     	    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/buy_e1_refurb.png")   {template_purchase(0, 0)}
    alternative_sprites(ss_e1_series_buy_refurb_livery_10,     	    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/buy_e1_refurb.png")   {template_purchase(0, 0)}

		switch (FEAT_TRAINS, SELF, ss_e1_series_buy_original_livery, param_spritelength) {
			1: ss_e1_series_buy_original_livery_10;					// Length 10
			ss_e1_series_buy_original_livery_12;					// Length 12
		}

		switch (FEAT_TRAINS, SELF, ss_e1_series_buy_refurb_livery, param_spritelength) {
			1: ss_e1_series_buy_refurb_livery_10;					// Length 10
			ss_e1_series_buy_refurb_livery_12;						// Length 12
		}

		switch(FEAT_TRAINS, SELF, sw_e1_series_year_dep_purchase, current_year) {
			0..2003: ss_e1_series_buy_original_livery;
			ss_e1_series_buy_refurb_livery;
		}

/* EMU Parts */

/* "CAB" */
    spriteset(ss_e1_series_cab_original_livery_12,             		    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_cab_rev_original_livery_12,             		"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_cab_refurb_livery_12,             		    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_cab_rev_refurb_livery_12,             		"src/trains/dummy/empty.png") {template_dummy(0,0)}

	spriteset(ss_e1_series_cab_original_livery_10,             		    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_cab_rev_original_livery_10,             		"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_cab_refurb_livery_10,             		    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_cab_rev_refurb_livery_10,             		"src/trains/dummy/empty.png") {template_dummy(0,0)}

    // E1 Series Original Livery
    alternative_sprites(ss_e1_series_cab_original_livery_12,     	    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/e1_original.png") {template_12_standard(1,1)}
    alternative_sprites(ss_e1_series_cab_rev_original_livery_12,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/e1_original.png") {template_12_reversed(1,41)}
	alternative_sprites(ss_e1_series_cab_original_livery_10,     	    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/10/e1_original.png") {template_10_standard(1,1)}
    alternative_sprites(ss_e1_series_cab_rev_original_livery_10,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/10/e1_original.png") {template_10_reversed(1,41)}		

    // E1 Series Refurbished Livery
    alternative_sprites(ss_e1_series_cab_refurb_livery_12,     		    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/e1_refurb.png") {template_12_standard(1,1)}
    alternative_sprites(ss_e1_series_cab_rev_refurb_livery_12,     		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/e1_refurb.png") {template_12_reversed(1,41)}
	alternative_sprites(ss_e1_series_cab_refurb_livery_10,     		    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/10/e1_refurb.png") {template_10_standard(1,1)}
    alternative_sprites(ss_e1_series_cab_rev_refurb_livery_10,     		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/10/e1_refurb.png") {template_10_reversed(1,41)}

		switch (FEAT_TRAINS, SELF, ss_e1_series_cab_original_livery, param_spritelength) {
			1: ss_e1_series_cab_original_livery_10;						// Length 10
			ss_e1_series_cab_original_livery_12;						// Length 12
		}

		switch (FEAT_TRAINS, SELF, ss_e1_series_cab_rev_original_livery, param_spritelength) {
			1: ss_e1_series_cab_rev_original_livery_10;					// Length 10
			ss_e1_series_cab_rev_original_livery_12;					// Length 12
		}

		switch (FEAT_TRAINS, SELF, ss_e1_series_cab_refurb_livery, param_spritelength) {
			1: ss_e1_series_cab_refurb_livery_10;						// Length 10
			ss_e1_series_cab_refurb_livery_12;							// Length 12
		}

		switch (FEAT_TRAINS, SELF, ss_e1_series_cab_rev_refurb_livery, param_spritelength) {
			1: ss_e1_series_cab_rev_refurb_livery_10;					// Length 10
			ss_e1_series_cab_rev_refurb_livery_12;						// Length 12
		}

/* COACH */
    spriteset(ss_e1_series_coach_original_livery_12,             		"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_coach_rev_original_livery_12,    			"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_coach_refurb_livery_12,             		    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_coach_rev_refurb_livery_12,    		    	"src/trains/dummy/empty.png") {template_dummy(0,0)}

	spriteset(ss_e1_series_coach_original_livery_10,             		"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_coach_rev_original_livery_10,    			"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_coach_refurb_livery_10,             		    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_coach_rev_refurb_livery_10,    		    	"src/trains/dummy/empty.png") {template_dummy(0,0)}

    // E1 Series Original Livery
    alternative_sprites(ss_e1_series_coach_original_livery_12,     		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/e1_original.png") {template_12_standard(1,121)}
    alternative_sprites(ss_e1_series_coach_rev_original_livery_12, 		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/e1_original.png") {template_12_reversed(1,121)}
	alternative_sprites(ss_e1_series_coach_original_livery_10,     		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/10/e1_original.png") {template_10_standard(1,121)}
    alternative_sprites(ss_e1_series_coach_rev_original_livery_10, 		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/10/e1_original.png") {template_10_reversed(1,121)}

    // E1 Series Refurbished Livery
    alternative_sprites(ss_e1_series_coach_refurb_livery_12,     		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/e1_refurb.png") {template_12_standard(1,121)}
    alternative_sprites(ss_e1_series_coach_rev_refurb_livery_12,   		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/e1_refurb.png") {template_12_reversed(1,121)}	
	alternative_sprites(ss_e1_series_coach_refurb_livery_10,     		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/10/e1_refurb.png") {template_10_standard(1,121)}
    alternative_sprites(ss_e1_series_coach_rev_refurb_livery_10,   		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/10/e1_refurb.png") {template_10_reversed(1,121)}	

		switch (FEAT_TRAINS, SELF, ss_e1_series_coach_original_livery, param_spritelength) {
			1: ss_e1_series_coach_original_livery_10;					// Length 10
			ss_e1_series_coach_original_livery_12;						// Length 12
		}

		switch (FEAT_TRAINS, SELF, ss_e1_series_coach_rev_original_livery, param_spritelength) {
			1: ss_e1_series_coach_rev_original_livery_10;				// Length 10
			ss_e1_series_coach_rev_original_livery_12;					// Length 12
		}

		switch (FEAT_TRAINS, SELF, ss_e1_series_coach_refurb_livery, param_spritelength) {
			1: ss_e1_series_coach_refurb_livery_10;						// Length 10
			ss_e1_series_coach_refurb_livery_12;						// Length 12
		}

		switch (FEAT_TRAINS, SELF, ss_e1_series_coach_rev_refurb_livery, param_spritelength) {
			1: ss_e1_series_coach_rev_refurb_livery_10;					// Length 10
			ss_e1_series_coach_rev_refurb_livery_12;					// Length 12
		}

/* PANTO */
    spriteset(ss_e1_series_panto_original_livery_12,          		   	"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_panto_rev_original_livery_12, 		   		"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_panto_refurb_livery_12,          		    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_panto_rev_refurb_livery_12, 		    		"src/trains/dummy/empty.png") {template_dummy(0,0)}

	spriteset(ss_e1_series_panto_original_livery_10,          		   	"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_panto_rev_original_livery_10, 		   		"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_panto_refurb_livery_10,          		    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e1_series_panto_rev_refurb_livery_10, 		    		"src/trains/dummy/empty.png") {template_dummy(0,0)}

    // E1 Series Original Livery
    alternative_sprites(ss_e1_series_panto_original_livery_12,     		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/e1_original.png") {template_12_standard(1,81)}
    alternative_sprites(ss_e1_series_panto_rev_original_livery_12, 		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/e1_original.png") {template_12_reversed(1,81)}
	alternative_sprites(ss_e1_series_panto_original_livery_10,     		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/10/e1_original.png") {template_10_standard(1,81)}
    alternative_sprites(ss_e1_series_panto_rev_original_livery_10, 		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/10/e1_original.png") {template_10_reversed(1,81)}	

    // E1 Series Refurbished Livery
    alternative_sprites(ss_e1_series_panto_refurb_livery_12,     		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/e1_refurb.png") {template_12_standard(1,81)}
    alternative_sprites(ss_e1_series_panto_rev_refurb_livery_12,   		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/12/e1_refurb.png") {template_12_reversed(1,81)}	
	alternative_sprites(ss_e1_series_panto_refurb_livery_10,     		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/10/e1_refurb.png") {template_10_standard(1,81)}
    alternative_sprites(ss_e1_series_panto_rev_refurb_livery_10,   		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e1_series/10/e1_refurb.png") {template_10_reversed(1,81)}	

		switch (FEAT_TRAINS, SELF, ss_e1_series_panto_original_livery, param_spritelength) {
			1: ss_e1_series_panto_original_livery_10;					// Length 10
			ss_e1_series_panto_original_livery_12;						// Length 12
		}

		switch (FEAT_TRAINS, SELF, ss_e1_series_panto_rev_original_livery, param_spritelength) {
			1: ss_e1_series_panto_rev_original_livery_10;				// Length 10
			ss_e1_series_panto_rev_original_livery_12;					// Length 12
		}

		switch (FEAT_TRAINS, SELF, ss_e1_series_panto_refurb_livery, param_spritelength) {
			1: ss_e1_series_panto_refurb_livery_10;						// Length 10
			ss_e1_series_panto_refurb_livery_12;						// Length 12
		}

		switch (FEAT_TRAINS, SELF, ss_e1_series_panto_rev_refurb_livery, param_spritelength) {
			1: ss_e1_series_panto_rev_refurb_livery_10;					// Length 10
			ss_e1_series_panto_rev_refurb_livery_12;					// Length 12
		}

/* LIVERIES */

// CAB

    switch(FEAT_TRAINS, SELF, sw_e1_series_cab_year_dep_livery, build_year) {
        0..2003: ss_e1_series_cab_original_livery;
        2004..9999: ss_e1_series_cab_refurb_livery;
        ss_e1_series_cab_original_livery;
    }

    switch(FEAT_TRAINS, SELF, sw_e1_series_cab, cargo_subtype) {
        1: ss_e1_series_cab_original_livery;
        2: ss_e1_series_cab_refurb_livery;
        sw_e1_series_cab_year_dep_livery;
    }

// CAB REVERSED

    switch(FEAT_TRAINS, SELF, sw_e1_series_cab_rev_year_dep_livery, build_year) {
        0..2003: ss_e1_series_cab_rev_original_livery;
        2004..9999: ss_e1_series_cab_rev_refurb_livery;
        ss_e1_series_cab_rev_original_livery;
    }

    switch(FEAT_TRAINS, PARENT, sw_e1_series_cab_rev, cargo_subtype) {
        1: ss_e1_series_cab_rev_original_livery;
        2: ss_e1_series_cab_rev_refurb_livery;
        sw_e1_series_cab_rev_year_dep_livery;
    }

// Coach

    switch(FEAT_TRAINS, SELF, sw_e1_series_coach_year_dep_livery, build_year) {
        0..2003: ss_e1_series_coach_original_livery;
        2004..9999: ss_e1_series_coach_refurb_livery;
        ss_e1_series_coach_original_livery;
    }

    switch(FEAT_TRAINS, PARENT, sw_e1_series_coach, cargo_subtype) {
        1: ss_e1_series_coach_original_livery;
        2: ss_e1_series_coach_refurb_livery;
        sw_e1_series_coach_year_dep_livery;
    }

// Coach Reversed

    switch(FEAT_TRAINS, SELF, sw_e1_series_coach_rev_year_dep_livery, build_year) {
        0..2003: ss_e1_series_coach_rev_original_livery;
        2004..9999: ss_e1_series_coach_rev_refurb_livery;
        ss_e1_series_coach_rev_original_livery;
    }

    switch(FEAT_TRAINS, PARENT, sw_e1_series_coach_rev, cargo_subtype) {
        1: ss_e1_series_coach_rev_original_livery;
        2: ss_e1_series_coach_rev_refurb_livery;
        sw_e1_series_coach_rev_year_dep_livery;
    }

// Panto

    switch(FEAT_TRAINS, SELF, sw_e1_series_panto_year_dep_livery, build_year) {
        0..2003: ss_e1_series_panto_original_livery;
        2004..9999: ss_e1_series_panto_refurb_livery;
        ss_e1_series_panto_original_livery;
    }

    switch(FEAT_TRAINS, PARENT, sw_e1_series_panto, cargo_subtype) {
        1: ss_e1_series_panto_original_livery;
        2: ss_e1_series_panto_refurb_livery;
        sw_e1_series_panto_year_dep_livery;
    }

// Panto rev

    switch(FEAT_TRAINS, SELF, sw_e1_series_panto_rev_year_dep_livery, build_year) {
        0..2003: ss_e1_series_panto_rev_original_livery;
        2004..9999: ss_e1_series_panto_rev_refurb_livery;
        ss_e1_series_panto_rev_original_livery;
    }

    switch(FEAT_TRAINS, PARENT, sw_e1_series_panto_rev, cargo_subtype) {
        1: ss_e1_series_panto_rev_original_livery;
        2: ss_e1_series_panto_rev_refurb_livery;
        sw_e1_series_panto_rev_year_dep_livery;
    }

/* FORMATIONS */

// Backwards
switch (FEAT_TRAINS, SELF, sw_e1_series_backwards_panto_in_middle, (position_in_consist == position_in_consist_from_end ) || (position_in_consist - 3 == position_in_consist_from_end)) {				
		0:  sw_e1_series_coach_rev;
		sw_e1_series_panto_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_e1_series_backwards_6th_and_3_from_end, position_in_consist == 7 || position_in_consist_from_end  == 16) {				
		0:  sw_e1_series_coach_rev;
		sw_e1_series_panto_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_e1_series_backwards, num_vehs_in_consist ) {				
		0..35:  sw_e1_series_backwards_panto_in_middle;
        sw_e1_series_backwards_6th_and_3_from_end;				
	}

// Forwards
switch (FEAT_TRAINS, SELF, sw_e1_series_forwards_panto_in_middle, (position_in_consist == position_in_consist_from_end ) || (position_in_consist - 3 == position_in_consist_from_end)) {				
		0:  sw_e1_series_coach;
		sw_e1_series_panto;				
	}

switch (FEAT_TRAINS, SELF, sw_e1_series_forwards_6th_and_3_from_end, position_in_consist == 16 || position_in_consist_from_end  == 7) {				
		0:  sw_e1_series_coach;
		sw_e1_series_panto;				
	}

switch (FEAT_TRAINS, SELF, sw_e1_series_forwards, num_vehs_in_consist ) {				
		0..35:  sw_e1_series_forwards_panto_in_middle;
        sw_e1_series_forwards_6th_and_3_from_end;				
	}

switch (FEAT_TRAINS, PARENT, sw_e1_series_direction, vehicle_is_reversed) {				
		0:  sw_e1_series_forwards;
		sw_e1_series_backwards;				
	}

switch (FEAT_TRAINS, SELF, sw_e1_series_back, position_in_consist_from_end) {				
		1:  sw_e1_series_cab_rev;
		sw_e1_series_direction;				
	}

switch (FEAT_TRAINS, SELF, sw_e1_series_front, position_in_consist) {				
		1:  sw_e1_series_cab;
		sw_e1_series_back;				
	}

switch (FEAT_TRAINS, SELF, sw_e1_series_graphics, position_in_consist % 3) {				
		1:  sw_e1_series_front; 				
		dummy_sprites;
	}

switch (FEAT_TRAINS, SELF, sw_e1_series_articulated_part, extra_callback_info1) {
		1 .. 2: return series_e1;									
		return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
	}

/*---Coupling Switch---*/
switch(FEAT_TRAINS, SELF, sw_e1_series_attach_vehid, [vehicle_type_id == mu_car_full]) {
 0: return string(ONLY_ADD_MUCAR);
}

/*---Length Check---*/
switch (FEAT_TRAINS, SELF, sw_e1_series_stop_start, num_vehs_in_consist < 12) {				
		1:  return string(STR_LENGTH_ERROR); 				
		return CB_RESULT_NO_TEXT; 
	}

item(FEAT_TRAINS, series_e1) {
	property {
		/* Menu */
		name: 							string(STR_NAME_E1_SERIES);
		sprite_id:                      SPRITE_ID_NEW_TRAIN; 		// required to enable new graphics
		misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
		running_cost_base:              RUNNING_COST_ELECTRIC;
		engine_class:                   ENGINE_CLASS_ELECTRIC;
		/* Availability */
		climates_available:             ALL_CLIMATES;
		introduction_date:              date(1994,07,15);
		model_life:                     18;							// In service until 2012,09,28
		vehicle_life:                   30;							// 
		track_type:                     SHINKANSEN;
		/* Cargo */
		reliability_decay:              5;
		refittable_cargo_classes:       bitmask(CC_PASSENGERS);
		refit_cost:                     0;
		default_cargo_type:             PASS;
		ai_special_flag:                AI_FLAG_PASSENGER;
		cargo_capacity:                 1;							// 
		loading_speed:                  50;							
		cargo_age_period:               278; 						// 1.5 times normal for this expensive long-distance train
		/* Stats */
		speed:                          241 km/h;					// Operational Top Speed
		power:                          550 * 4 * 6 hp;					// 550 hp (or 410 kW) per motor x 4 motors per car x 6 motorised cars
		weight:                         58 * 12 tons;					// 58t (per JPS) x 12 cars
		tractive_effort_coefficient:    0.3;
		air_drag_coefficient:			0;
	}
	graphics {
		/* Menu */
		additional_text:                string(STR_DESC_E1_SERIES);
		cargo_subtype_text:             sw_e1_series_year_dep_subtype_text;
		can_attach_wagon:				sw_e1_series_attach_vehid;
		start_stop:						sw_e1_series_stop_start;
		purchase_weight:                58;
        purchase_power:                 550 * 4;
		/* Graphics */
		default:                        sw_e1_series_graphics;
		purchase:                       sw_e1_series_year_dep_purchase;
		articulated_part:               sw_e1_series_articulated_part;
		visual_effect_and_powered:      sw_visual_effect_and_powered;
		/* Stats */
		length:                         sw_param_length;
		power:							NumberCarsInConsist() / 2 * 550 * 4;	// 410 kW
		weight:							NumberCarsInConsist() * 58;
		cargo_capacity:                 34 * CapacityFactor();										// 1235 capacity / (12 cars * 3 parts) is approx 35						
		loading_speed:					34 * CapacityFactor() / param_loading_2D;					// Capacity divided by door loading factor
		cargo_age_period:				param_decay_express;
		cost_factor:                    128;
		running_cost_factor:            (295 + 122 * NumberCarsInConsist() ) * RunningCostFactor();
		purchase_running_cost_factor:   295 + 122 * NumberCarsInConsist();
	}
	/*Multiple Unit Car*/
	
      livery_override(mu_car_full){
        default:                        sw_e1_series_graphics;
        cargo_capacity:                 34 * CapacityFactor();
        cargo_age_period:				param_decay_express;
        loading_speed:					34 * CapacityFactor() / param_loading_2D;
		articulated_part:				sw_mucar_full_articulated_part;
		length:							sw_param_length;
		//power:						// Apply power in graphics block to calculate power based on number of motorised cars correctly
		//weight:						// Apply weight to graphics block to calculate Max T.E. correctly
        //running_cost_factor:            sw_e1_series_running_costs;
      }
}

/* End E1 series */
