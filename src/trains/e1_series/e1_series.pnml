/* Begin E1 Series */

#include "src/trains/e1_series/e1_series_subtypes.pnml"
#include "src/trains/e1_series/e1_series_gfx.pnml"
#include "src/trains/e1_series/e1_series_liveries.pnml"

switch (FEAT_TRAINS, SELF, sw_e1_series_backwards, position_in_consist_from_end % 36) {				
		16:  ss_e1_series_panto_rev;
		28: ss_e1_series_panto_rev;
		ss_e1_series_coach_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_e1_series_forwards, position_in_consist % 36) {				
		16:  ss_e1_series_panto;
		28: ss_e1_series_panto;
		ss_e1_series_coach;				
	}

switch (FEAT_TRAINS, PARENT, sw_e1_series_direction, vehicle_is_reversed) {				
		0:  sw_e1_series_forwards;
		sw_e1_series_backwards;				
	}

switch (FEAT_TRAINS, SELF, sw_e1_series_back, position_in_consist_from_end) {				
		1:  ss_e1_series_cab_rev;
		sw_e1_series_direction;				
	}

switch (FEAT_TRAINS, SELF, sw_e1_series_front, position_in_consist) {				
		1:  ss_e1_series_cab;
		sw_e1_series_back;				
	}

switch (FEAT_TRAINS, SELF, sw_e1_series_graphics, position_in_consist % 3) {				
		1:  sw_e1_series_front; 				
		dummy_sprites;
	}

switch (FEAT_TRAINS, SELF, sw_e1_series_articulated_part, extra_callback_info1) {
		1 .. 2: return series_e1;									
		return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
	}

/*---Coupling Switch---*/
switch(FEAT_TRAINS, SELF, sw_e1_series_attach_vehid, [vehicle_type_id == mu_car_full]) {
 0: return string(ONLY_ADD_MUCAR);
}

/*---Length Check---*/
switch (FEAT_TRAINS, SELF, sw_e1_series_stop_start, num_vehs_in_consist < 12) {				
		1:  return string(STR_LENGTH_ERROR); 				
		return CB_RESULT_NO_TEXT; 
	}

item(FEAT_TRAINS, series_e1) {
	property {
		/* Menu */
		name: 							string(STR_NAME_E1_SERIES);
		sprite_id:                      SPRITE_ID_NEW_TRAIN; 		// required to enable new graphics
		misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
		running_cost_base:              RUNNING_COST_ELECTRIC;
		engine_class:                   ENGINE_CLASS_ELECTRIC;
		/* Availability */
		climates_available:             ALL_CLIMATES;
		introduction_date:              date(1994,07,15);
		model_life:                     18;							// In service until 2012,09,28
		vehicle_life:                   30;							// 
		track_type:                     SHINKANSEN;
		/* Cargo */
		reliability_decay:              5;
		refittable_cargo_classes:       bitmask(CC_PASSENGERS);
		refit_cost:                     0;
		default_cargo_type:             PASS;
		ai_special_flag:                AI_FLAG_PASSENGER;
		cargo_capacity:                 1;							// 
		loading_speed:                  50;							
		cargo_age_period:               278; 						// 1.5 times normal for this expensive long-distance train
		/* Stats */
		speed:                          241 km/h;					// Operational Top Speed
		power:                          550 * 4 * 6 hp;					// 550 hp (or 410 kW) per motor x 4 motors per car x 6 motorised cars
		weight:                         58 * 12 tons;					// 58t (per JPS) x 12 cars
		tractive_effort_coefficient:    0.3;
		air_drag_coefficient:			0;
	}
	graphics {
		/* Menu */
		additional_text:                string(STR_DESC_E1_SERIES);
		cargo_subtype_text:             sw_e1_series_year_dep_subtype_text;
		can_attach_wagon:				sw_e1_series_attach_vehid;
		start_stop:						sw_e1_series_stop_start;
		/* Graphics */
		default:                        sw_e1_series_graphics;
		purchase:                       sw_e1_series_year_dep_purchase;
		articulated_part:               sw_e1_series_articulated_part;
		visual_effect_and_powered:      sw_visual_effect_and_powered;
		/* Stats */
		length:                         sw_length_2_8_2;
		power:							NumberCarsInConsist() / 2 * 550 * 4;	// 410 kW
		weight:							NumberCarsInConsist() * 58;
		cargo_capacity:                 34 * CapacityFactor();										// 1235 capacity / (12 cars * 3 parts) is approx 35						
		loading_speed:					34 * CapacityFactor() / param_loading_2D;					// Capacity divided by door loading factor
		cargo_age_period:				param_decay_express;
		cost_factor:                    128;
		running_cost_factor:            (95 + 45 * NumberCarsInConsist() ) * RunningCostFactor();
		purchase_running_cost_factor:   95 + 45 * NumberCarsInConsist();
	}
	/*Multiple Unit Car*/
	
      livery_override(mu_car_full){
        default:                        sw_e1_series_graphics;
        cargo_capacity:                 34 * CapacityFactor();
        cargo_age_period:				param_decay_express;
        loading_speed:					34 * CapacityFactor() / param_loading_2D;
		articulated_part:				sw_mucar_full_articulated_part;
		length:							sw_length_2_8_2;
		//power:						// Apply power in graphics block to calculate power based on number of motorised cars correctly
		//weight:						// Apply weight to graphics block to calculate Max T.E. correctly
        //running_cost_factor:            sw_e1_series_running_costs;
      }
}

/* End E1 series */
