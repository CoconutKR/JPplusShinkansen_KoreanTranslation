/* Begin e5&e6 Series */

/* CARGO SUBTYPE MENU*/

// From 2013 to 2015 - launch of E6 Series (E5 already launched)
switch(FEAT_TRAINS, SELF, sw_e5e6_series_2013_subtype_text, cargo_subtype) {
	0: return string(LV_E5E6_SERIES_PINK);     
       return CB_RESULT_NO_TEXT;
    }

// From 2016 onwards - launch of H5
switch(FEAT_TRAINS, SELF, sw_e5e6_series_2016_subtype_text, cargo_subtype) {
	0: return string(LV_E5E6_SERIES_PINK);   
    1: return string(LV_E5E6_SERIES_PURPLE);
       return CB_RESULT_NO_TEXT;
    }

switch(FEAT_TRAINS, SELF, sw_e5e6_series_year_dep_subtype_text, current_year) {
    0..2015: sw_e5e6_series_2013_subtype_text;
    sw_e5e6_series_2016_subtype_text;
}

/* SPRITES */

/* Purchase Menu */
spriteset (ss_e5e6_series_purchase,	        	"src/gfx/dummy/purchase.png") {template_purchase(0, 0)}
alternative_sprites(ss_e5e6_series_purchase,    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/gfx/e5_series/buy_e5e6.png") {template_purchase(0, 0)}

/* PUT TOGETHER THE EMU */

/* Select the correct part of the train */
switch (FEAT_TRAINS, SELF, sw_e5e6_series_graphics_forwards, position_in_consist) {
					
	1:  ss_e5_series_cab; 		        				// Car 1							
	7:  ss_e5_series_coach;		        				// Car 2    							
	13: ss_e5_series_panto;                				// Car 3
	19: ss_e5_series_coach;                				// Car 4
    25: ss_e5_series_coach;                				// Car 5
    31: ss_e5_series_coach;                				// Car 6
    37: ss_e5_series_panto;                				// Car 7
	43: ss_e5_series_coach;                				// Car 8
    49: ss_e5_series_coach;                				// Car 9
	55: ss_e5_series_cab_rev;                    		// Car 10
    60: spriteset_e6_series_front_std;          		// Car 11
    64: spriteset_e6_series_rev_mid_panto_std;  		// Car 12
    68: spriteset_e6_series_rev_mid_std;				// Car 13
    72: spriteset_e6_series_rev_mid_std;				// Car 14
    76: spriteset_e6_series_rev_mid_std;				// Car 15
    80: spriteset_e6_series_rev_mid_panto_std;			// Car 16
    84: spriteset_e6_series_revflip_front_std;			// Car 17
	dummy_sprites;
}

switch (FEAT_TRAINS, SELF, sw_e5e6_series_graphics_backwards, position_in_consist) {
					
	0: spriteset_e6_series_front_std; 					// Car 17							
	4: spriteset_e6_series_revflip_mid_panto_std;		// Car 16   							
	8: spriteset_e6_series_revflip_mid_std;     		// Car 15
	12: spriteset_e6_series_revflip_mid_std;    		// Car 14
    16: spriteset_e6_series_revflip_mid_std;    		// Car 13
    20: spriteset_e6_series_revflip_mid_panto_std;      // Car 12
    24: spriteset_e6_series_revflip_front_std;     		// Car 11
	29: ss_e5_series_cab;                 				// Car 10
    35: ss_e5_series_coach_reversed;       				// Car 9
	41: ss_e5_series_coach_reversed;       				// Car 8
    47: ss_e5_series_panto_reversed;       				// Car 7
    53: ss_e5_series_coach_reversed;       				// Car 6
    59: ss_e5_series_coach_reversed;       				// Car 5
    65: ss_e5_series_coach_reversed;       				// Car 4
    71: ss_e5_series_panto_reversed;       				// Car 3
    77: ss_e5_series_coach_reversed;       				// Car 2
    83: ss_e5_series_cab_rev;                 			// Car 1
	dummy_sprites;
}

switch (FEAT_TRAINS, PARENT, sw_e5e6_series_graphics, vehicle_is_reversed) {				
	1:  sw_e5e6_series_graphics_backwards; 		            							
	sw_e5e6_series_graphics_forwards;
}

/* Assemble an 17-car articulated vehicle. */
switch (FEAT_TRAINS, SELF, sw_e5e6_series_articulated_part, extra_callback_info1) {
	1 .. 87: return series_e5e6;										// Should be same as range above
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, sw_e5e6_series_visual_effect_and_powered, ((position_in_articulated_veh == 0) && !func_is_reversed()) || ((position_in_articulated_veh_from_end == 0) && func_is_reversed()) ) {
	1: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 0, ENABLE_WAGON_POWER);
	return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

// Cargo Capacity

// Base Capacities for each parameter level
switch(FEAT_TRAINS, SELF, sw_e5e6_series_param_cargo_capacity, param_basecapacity) {
    // Assuming 65 capacity per 0.5 tile 
    0: return 52;       // Low Capacity
    2: return 78;       // High Capacity
    3: return 91;       // Very High Capacity
    return 65;          // Normal Capacity 
}

/* Base Capacity x 27 / 100 x 88 = Total Passenger Capacity

 * 27/100 is what adjusts the capacities to give a capacity in line with JapanSet 3
 * 27 was chosen so that 65 x 27 / 100 > 16.25 which is the factor that matches 1430 (the capacity of an 11.0 tile JapanSet E5 Series Shinkansen)
 * 88 is the number of sprites in a joint E5 and E6 Series Shinkansen*/

switch(FEAT_TRAINS, SELF, sw_e5e6_series_cargo_capacity, (sw_e5e6_series_param_cargo_capacity() * 27 ) / 100 )
    {return;}



item(FEAT_TRAINS, series_e5e6, 2056) {
	property {
		name: 							string(STR_NAME_E5E6_SERIES);
		climates_available:             ALL_CLIMATES;
		introduction_date:              date(2013,03,16); 
		model_life:                     VEHICLE_NEVER_EXPIRES;		//
		vehicle_life:                   30;							// 
		reliability_decay:              5;
		refittable_cargo_classes:       bitmask(CC_PASSENGERS);
		refit_cost:                     0;
		default_cargo_type:             PASS;
		cargo_capacity:                 1;							// 
		loading_speed:                  50;							
		cargo_age_period:               278; 						// 1.5 times normal for this expensive long-distance train
		sprite_id:                      SPRITE_ID_NEW_TRAIN; 		// required to enable new graphics
		speed:                          321 km/h;				// Operational Top Speed
		misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
		track_type:                     SHINKANSEN;
		ai_special_flag:                AI_FLAG_PASSENGER;
		running_cost_base:              RUNNING_COST_STEAM;
		engine_class:                   ENGINE_CLASS_ELECTRIC;
		power:                          8050 kW;					// Agreed to Wikipedia
		weight:                         302 tons;					// Agreed to Wikipedia 
		tractive_effort_coefficient:    0.3;

	}
	graphics {
		default:                        sw_e5e6_series_graphics;
		purchase:                       ss_e5e6_series_purchase;
		articulated_part:               sw_e5e6_series_articulated_part;
		length:                         2;
		cargo_capacity:                 sw_e5e6_series_cargo_capacity;
		//colour_mapping:                 sw_cc_blue_white;s
		cargo_subtype_text:             sw_e5e6_series_year_dep_subtype_text;
		cost_factor:                    257;
		running_cost_factor:            func_running_costs(300);
		purchase_running_cost_factor:   300;
		visual_effect_and_powered:      sw_e5e6_series_visual_effect_and_powered;
		additional_text:                string(STR_DESC_E5E6_SERIES);
	}
}


/* End e5&e6 Series */
