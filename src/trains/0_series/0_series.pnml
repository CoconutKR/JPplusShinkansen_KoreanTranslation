/* Begin 0 Series */

#include "src/trains/0_series/0_series_subtypes.pnml"
#include "src/trains/0_series/0_series_gfx.pnml"
#include "src/trains/0_series/0_series_liveries.pnml"
#include "src/trains/0_series/0_series_stats.pnml"

switch (FEAT_TRAINS, SELF, sw_0_series_backwards, position_in_consist_from_end % 6) {				
		4:  ss_0_series_panto_rev;
		ss_0_series_coach_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_0_series_forwards, position_in_consist % 6) {				
		4:  ss_0_series_panto;
		ss_0_series_coach;				
	}

switch (FEAT_TRAINS, PARENT, sw_0_series_direction, vehicle_is_reversed) {				
		0:  sw_0_series_forwards;
		sw_0_series_backwards;				
	}

switch (FEAT_TRAINS, SELF, sw_0_series_back, position_in_consist_from_end) {				
		1:  ss_0_series_cab_rev;
		sw_0_series_direction;				
	}

switch (FEAT_TRAINS, SELF, sw_0_series_front, position_in_consist) {				
		1:  ss_0_series_cab;
		sw_0_series_back;				
	}

switch (FEAT_TRAINS, SELF, sw_0_series_graphics, position_in_consist % 3) {				
		1:  sw_0_series_front; 				
		dummy_sprites;
	}

switch (FEAT_TRAINS, SELF, sw_0_series_articulated_part, extra_callback_info1) {
		1 .. 2: return series_0;									
		return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
	}

/*---Coupling Switch---*/
switch(FEAT_TRAINS, SELF, sw_0_series_attach_vehid, [vehicle_type_id == mu_car_full]) {
 0: return string(ONLY_ADD_MUCAR);
}

/*---Length Check---*/
switch (FEAT_TRAINS, SELF, sw_0_series_stop_start, num_vehs_in_consist < 12) {				
		1:  return string(STR_LENGTH_ERROR); 				
		return CB_RESULT_NO_TEXT; 
	}

item(FEAT_TRAINS, series_0) {
	property {
		/* Menu */
        name: 							string(STR_NAME_0_SERIES);
        sprite_id:                      SPRITE_ID_NEW_TRAIN; 		// required to enable new graphics
        misc_flags:                     bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
		running_cost_base:              RUNNING_COST_ELECTRIC;
		engine_class:                   ENGINE_CLASS_ELECTRIC;
        /* Availability */
		climates_available:             ALL_CLIMATES;
		introduction_date:              date(1964,10,01);           // Date of launch
		model_life:                     2008 - 1964;		        // Until 2008
		vehicle_life:                   30;							// Same as JapanSet
        track_type:                     SHINKANSEN;
		/* Cargo */
        reliability_decay:              5;
		refittable_cargo_classes:       bitmask(CC_PASSENGERS);
		refit_cost:                     0;
		default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
		cargo_capacity:                 1;							// 
		loading_speed:                  50;                         // Changed by callback						
		cargo_age_period:               278; 						// Default 185 x 1.5
		/* Stats */
        speed:                          211 km/h;                   // Changed by callback
        power:                          8050 kW;	                // Changed by callback				
		weight:                         1464 ton;                   // Changed by callback					
		tractive_effort_coefficient:    0.3;                        // Standard
		air_drag_coefficient:			0;
	}
	graphics {
		/* Menu */
        additional_text:                string(STR_DESC_0_SERIES);
        cargo_subtype_text:             sw_0_series_year_dep_subtype_text;
		can_attach_wagon:				sw_0_series_attach_vehid;
		start_stop:						sw_0_series_stop_start;
        /* Graphics */
        default:                        sw_0_series_graphics;
		purchase:                       sw_0_series_year_dep_purchase;
		articulated_part:               sw_0_series_articulated_part;
        visual_effect_and_powered:      sw_visual_effect_and_powered;
		/* Stats */
        length:                         sw_length_2_8_2;
        weight:                         sw_0_series_weight;
        power:                          sw_0_series_power;
        speed:							sw_0_series_speed;
		cargo_capacity:                 sw_0_series_cargo_capacity;
		loading_speed:					sw_0_series_loading_speed;
		cargo_age_period:				param_decay_express;
        cost_factor:                    125;
		running_cost_factor:            sw_0_series_running_costs;
		purchase_running_cost_factor:   running_cost_0_series;
	}
	/*Multiple Unit Car*/
	
      livery_override(mu_car_full){
        default:                        sw_0_series_graphics;
        cargo_capacity:                 sw_0_series_cargo_capacity;
        cargo_age_period:				param_decay_express;
        loading_speed:                  sw_0_series_loading_speed;
		articulated_part:				sw_mucar_full_articulated_part;
		length:							sw_length_2_8_2;
		//power:						// Apply power in graphics block to calculate power based on number of motorised cars correctly
		//weight:						// Apply weight to graphics block to calculate Max T.E. correctly
        running_cost_factor:            sw_0_series_running_costs;
      }
}

/* End 0 series */
