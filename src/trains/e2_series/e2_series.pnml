/* Begin E2 Series */

/* SUBTYPES */

    // From 1997 to 2002
    switch(FEAT_TRAINS, SELF, sw_e2_series_1997_subtype_text, cargo_subtype) {
        0: return string(LV_YEAR_DEP_LIVERY);
        1: return string(LV_E2_SERIES_RED);
        return CB_RESULT_NO_TEXT;
        }

    // From 2002 onwards
    switch(FEAT_TRAINS, SELF, sw_e2_series_2002_subtype_text, cargo_subtype) {
        0: return string(LV_YEAR_DEP_LIVERY);
        1: return string(LV_E2_SERIES_RED);
        2: return string(LV_E2_SERIES_PINK);
        return CB_RESULT_NO_TEXT;
        }

    switch(FEAT_TRAINS, SELF, sw_e2_series_year_dep_subtype_text, current_year) {
        0..2001: sw_e2_series_1997_subtype_text;
        sw_e2_series_2002_subtype_text;
    }

/* GRAPHICS */

    // Purchase Menu
    spriteset (ss_e2_series_buy_red_12,	        	            "src/trains/dummy/purchase.png") {template_purchase(0, 0)}
    spriteset (ss_e2_series_buy_red_10,	        	            "src/trains/dummy/purchase.png") {template_purchase(0, 0)}
    alternative_sprites(ss_e2_series_buy_red_12,                ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/buy_red.png") {template_purchase(0, 0)}
    alternative_sprites(ss_e2_series_buy_red_10,                ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/buy_red.png") {template_purchase(0, 0)}

    spriteset (ss_e2_series_buy_pink_12,	        	        "src/trains/dummy/purchase.png") {template_purchase(0, 0)}
    spriteset (ss_e2_series_buy_pink_10,	        	        "src/trains/dummy/purchase.png") {template_purchase(0, 0)}
    alternative_sprites(ss_e2_series_buy_pink_12,               ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/buy_pink.png") {template_purchase(0, 0)}
    alternative_sprites(ss_e2_series_buy_pink_10,               ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/buy_pink.png") {template_purchase(0, 0)}

        switch (FEAT_TRAINS, SELF, ss_e2_series_buy_red, param_spritelength) {
			1: ss_e2_series_buy_red_10;						    // Length 10
			ss_e2_series_buy_red_12;						    // Length 12
		}

		switch (FEAT_TRAINS, SELF, ss_e2_series_buy_pink, param_spritelength) {
			1: ss_e2_series_buy_pink_10;					    // Length 10
			ss_e2_series_buy_pink_12;						    // Length 12
		}

    // Red Stripe

    // "CAB"
    spriteset(ss_e2_series_cab_red_livery_12,             		"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e2_series_cab_rev_red_livery_12,             	"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e2_series_cab_red_livery_10,             		"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e2_series_cab_rev_red_livery_10,             	"src/trains/dummy/empty.png") {template_dummy(0,0)}

    alternative_sprites(ss_e2_series_cab_red_livery_12,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/red_stripe.png") {template_12_standard(1,1)}
    alternative_sprites(ss_e2_series_cab_rev_red_livery_12,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/red_stripe.png") {template_12_reversed(1,41)}
    alternative_sprites(ss_e2_series_cab_red_livery_10,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/10/red_stripe.png") {template_10_standard(1,1)}
    alternative_sprites(ss_e2_series_cab_rev_red_livery_10,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/10/red_stripe.png") {template_10_reversed(1,41)}

        switch (FEAT_TRAINS, SELF, ss_e2_series_cab_red_livery, param_spritelength) {
			1: ss_e2_series_cab_red_livery_10;					// Length 10
			ss_e2_series_cab_red_livery_12;						// Length 12
		}

        switch (FEAT_TRAINS, SELF, ss_e2_series_cab_rev_red_livery, param_spritelength) {
			1: ss_e2_series_cab_rev_red_livery_10;				// Length 10
			ss_e2_series_cab_rev_red_livery_12;					// Length 12
		}

    // COACH
    spriteset(ss_e2_series_coach_red_livery_12,             	"src/trains/dummy/empty.png"){template_dummy(0,0)}
    spriteset(ss_e2_series_coach_rev_red_livery_12,    		    "src/trains/dummy/empty.png"){template_dummy(0,0)}
    spriteset(ss_e2_series_coach_red_livery_10,             	"src/trains/dummy/empty.png"){template_dummy(0,0)}
    spriteset(ss_e2_series_coach_rev_red_livery_10,    		    "src/trains/dummy/empty.png"){template_dummy(0,0)}

    alternative_sprites(ss_e2_series_coach_red_livery_12,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/red_stripe.png") {template_12_standard(1,121)}
    alternative_sprites(ss_e2_series_coach_rev_red_livery_12,   ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/red_stripe.png") {template_12_reversed(1,121)}
    alternative_sprites(ss_e2_series_coach_red_livery_10,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/10/red_stripe.png") {template_10_standard(1,121)}
    alternative_sprites(ss_e2_series_coach_rev_red_livery_10,   ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/10/red_stripe.png") {template_10_reversed(1,121)}

        switch (FEAT_TRAINS, SELF, ss_e2_series_coach_red_livery, param_spritelength) {
			1: ss_e2_series_coach_red_livery_10;				// Length 10
			ss_e2_series_coach_red_livery_12;					// Length 12
		}

        switch (FEAT_TRAINS, SELF, ss_e2_series_coach_rev_red_livery, param_spritelength) {
			1: ss_e2_series_coach_rev_red_livery_10;			// Length 10
			ss_e2_series_coach_rev_red_livery_12;				// Length 12
		}

    // COACH PANTO
    spriteset(ss_e2_series_panto_red_livery_12,          	    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e2_series_panto_rev_red_livery_12, 		    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e2_series_panto_red_livery_10,          	    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e2_series_panto_rev_red_livery_10, 		    "src/trains/dummy/empty.png") {template_dummy(0,0)}

    alternative_sprites(ss_e2_series_panto_red_livery_12,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/red_stripe.png") {template_12_standard(1,81)}
    alternative_sprites(ss_e2_series_panto_rev_red_livery_12,   ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/red_stripe.png") {template_12_reversed(1,81)}
    alternative_sprites(ss_e2_series_panto_red_livery_10,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/10/red_stripe.png") {template_10_standard(1,81)}
    alternative_sprites(ss_e2_series_panto_rev_red_livery_10,   ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/10/red_stripe.png") {template_10_reversed(1,81)}

        switch (FEAT_TRAINS, SELF, ss_e2_series_panto_red_livery, param_spritelength) {
			1: ss_e2_series_panto_red_livery_10;				// Length 10
			ss_e2_series_panto_red_livery_12;					// Length 12
		}

        switch (FEAT_TRAINS, SELF, ss_e2_series_panto_rev_red_livery, param_spritelength) {
			1: ss_e2_series_panto_rev_red_livery_10;			// Length 10
			ss_e2_series_panto_rev_red_livery_12;				// Length 12
		}

    // Pink Stripe

    // "CAB"
    spriteset(ss_e2_series_cab_pink_livery_12,             	    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e2_series_cab_rev_pink_livery_12,              "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e2_series_cab_pink_livery_10,             	    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e2_series_cab_rev_pink_livery_10,              "src/trains/dummy/empty.png") {template_dummy(0,0)}

    alternative_sprites(ss_e2_series_cab_pink_livery_12,        ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/pink_stripe.png") {template_12_standard(1,1)}
    alternative_sprites(ss_e2_series_cab_rev_pink_livery_12,    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/pink_stripe.png") {template_12_reversed(1,41)}
    alternative_sprites(ss_e2_series_cab_pink_livery_10,        ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/10/pink_stripe.png") {template_10_standard(1,1)}
    alternative_sprites(ss_e2_series_cab_rev_pink_livery_10,    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/10/pink_stripe.png") {template_10_reversed(1,41)}

        switch (FEAT_TRAINS, SELF, ss_e2_series_cab_pink_livery, param_spritelength) {
			1: ss_e2_series_cab_pink_livery_10;					// Length 10
			ss_e2_series_cab_pink_livery_12;					// Length 12
		}

        switch (FEAT_TRAINS, SELF, ss_e2_series_cab_rev_pink_livery, param_spritelength) {
			1: ss_e2_series_cab_rev_pink_livery_10;				// Length 10
			ss_e2_series_cab_rev_pink_livery_12;				// Length 12
		}

    // COACH
    spriteset(ss_e2_series_coach_pink_livery_12,             	"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e2_series_coach_rev_pink_livery_12,    		"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e2_series_coach_pink_livery_10,             	"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e2_series_coach_rev_pink_livery_10,    		"src/trains/dummy/empty.png") {template_dummy(0,0)}

    alternative_sprites(ss_e2_series_coach_pink_livery_12,      ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/pink_stripe.png") {template_12_standard(1,121)}
    alternative_sprites(ss_e2_series_coach_rev_pink_livery_12,  ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/pink_stripe.png") {template_12_reversed(1,121)}
    alternative_sprites(ss_e2_series_coach_pink_livery_10,      ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/10/pink_stripe.png") {template_10_standard(1,121)}
    alternative_sprites(ss_e2_series_coach_rev_pink_livery_10,  ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/10/pink_stripe.png") {template_10_reversed(1,121)}

        switch (FEAT_TRAINS, SELF, ss_e2_series_coach_pink_livery, param_spritelength) {
			1: ss_e2_series_coach_pink_livery_10;				// Length 10
			ss_e2_series_coach_pink_livery_12;					// Length 12
		}

        switch (FEAT_TRAINS, SELF, ss_e2_series_coach_rev_pink_livery, param_spritelength) {
			1: ss_e2_series_coach_rev_pink_livery_10;			// Length 10
			ss_e2_series_coach_rev_pink_livery_12;				// Length 12
		}

    // COACH PANTO
    spriteset(ss_e2_series_panto_pink_livery_12,          		"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e2_series_panto_rev_pink_livery_12, 		    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e2_series_panto_pink_livery_10,          		"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_e2_series_panto_rev_pink_livery_10, 		    "src/trains/dummy/empty.png") {template_dummy(0,0)}

    alternative_sprites(ss_e2_series_panto_pink_livery_12,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/pink_stripe.png") {template_12_standard(1,81)}
    alternative_sprites(ss_e2_series_panto_rev_pink_livery_12,  ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/12/pink_stripe.png") {template_12_reversed(1,81)}
    alternative_sprites(ss_e2_series_panto_pink_livery_10,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/10/pink_stripe.png") {template_10_standard(1,81)}
    alternative_sprites(ss_e2_series_panto_rev_pink_livery_10,  ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e2_series/10/pink_stripe.png") {template_10_reversed(1,81)}

        switch (FEAT_TRAINS, SELF, ss_e2_series_panto_pink_livery, param_spritelength) {
			1: ss_e2_series_panto_pink_livery_10;				// Length 10
			ss_e2_series_panto_pink_livery_12;					// Length 12
		}

        switch (FEAT_TRAINS, SELF, ss_e2_series_panto_rev_pink_livery, param_spritelength) {
			1: ss_e2_series_panto_rev_pink_livery_10;			// Length 10
			ss_e2_series_panto_rev_pink_livery_12;				// Length 12
		}   

/* LIVERIES */

// PURCHASE MENU

switch(FEAT_TRAINS, SELF, sw_e2_series_buy, build_year) {
    0..2001: ss_e2_series_buy_red;
    2002..9999: ss_e2_series_buy_pink;
    ss_e2_series_buy_red;
}

// CAB

switch(FEAT_TRAINS, SELF, sw_e2_series_cab_year_dep_livery, build_year) {
    0..2001: ss_e2_series_cab_red_livery;
    2002..9999: ss_e2_series_cab_pink_livery;
    ss_e2_series_cab_red_livery;
}

switch(FEAT_TRAINS, SELF, ss_e2_series_cab, cargo_subtype) {
    1: ss_e2_series_cab_red_livery;
    2: ss_e2_series_cab_pink_livery;
    sw_e2_series_cab_year_dep_livery;
}

// CAB REVERSED

switch(FEAT_TRAINS, SELF, sw_e2_series_cab_rev_year_dep_livery, build_year) {
    0..2001: ss_e2_series_cab_rev_red_livery;
    2002..9999: ss_e2_series_cab_rev_pink_livery;
    ss_e2_series_cab_rev_red_livery;
}

switch(FEAT_TRAINS, SELF, ss_e2_series_cab_rev, cargo_subtype) {
    1: ss_e2_series_cab_rev_red_livery;
    2: ss_e2_series_cab_rev_pink_livery;
    sw_e2_series_cab_rev_year_dep_livery;
}

// COACH

switch(FEAT_TRAINS, SELF, sw_e2_series_coach_year_dep_livery, build_year) {
    0..2001: ss_e2_series_coach_red_livery;
    2002..9999: ss_e2_series_coach_pink_livery;
    ss_e2_series_coach_red_livery;
}

switch(FEAT_TRAINS, SELF, ss_e2_series_coach, cargo_subtype) {
    1: ss_e2_series_coach_red_livery;
    2: ss_e2_series_coach_pink_livery;
    sw_e2_series_coach_year_dep_livery;
}

// Coach Reversed

switch(FEAT_TRAINS, SELF, sw_e2_series_coach_rev_year_dep_livery, build_year) {
    0..2001: ss_e2_series_coach_rev_red_livery;
    2002..9999: ss_e2_series_coach_rev_pink_livery;
    ss_e2_series_coach_rev_red_livery;
}

switch(FEAT_TRAINS, SELF, ss_e2_series_coach_rev, cargo_subtype) {
    1: ss_e2_series_coach_rev_red_livery;
    2: ss_e2_series_coach_rev_pink_livery;
    sw_e2_series_coach_rev_year_dep_livery;
}

// Panto

switch(FEAT_TRAINS, SELF, sw_e2_series_panto_year_dep_livery, build_year) {
    0..2001: ss_e2_series_panto_red_livery;
    2002..9999: ss_e2_series_panto_pink_livery;
    ss_e2_series_panto_red_livery;
}

switch(FEAT_TRAINS, SELF, ss_e2_series_panto, cargo_subtype) {
    1: ss_e2_series_panto_red_livery;
    2: ss_e2_series_panto_pink_livery;
    sw_e2_series_panto_year_dep_livery;
}

// Panto Reversed

switch(FEAT_TRAINS, SELF, sw_e2_series_panto_rev_year_dep_livery, build_year) {
    0..2001: ss_e2_series_panto_rev_red_livery;
    2002..9999: ss_e2_series_panto_rev_pink_livery;
    ss_e2_series_panto_rev_red_livery;
}

switch(FEAT_TRAINS, SELF, ss_e2_series_panto_rev, cargo_subtype) {
    1: ss_e2_series_panto_rev_red_livery;
    2: ss_e2_series_panto_rev_pink_livery;
    sw_e2_series_panto_rev_year_dep_livery;
}

/* FORMATIONS */

// Forwards
switch (FEAT_TRAINS, SELF, sw_e2_series_forwards_panto_in_middle, (position_in_consist == position_in_consist_from_end ) || (position_in_consist - 3 == position_in_consist_from_end)) {				
		0:  ss_e2_series_coach;
		ss_e2_series_panto;				
	}

switch (FEAT_TRAINS, SELF, sw_e2_series_forwards_4th_and_6th, position_in_consist == 10 || position_in_consist  == 16) {				
		0:  ss_e2_series_coach;
		ss_e2_series_panto;				
	}

switch (FEAT_TRAINS, SELF, sw_e2_series_forwards_4th_and_5_from_end, position_in_consist == 10 || position_in_consist_from_end  == 13) {				
		0:  ss_e2_series_coach;
		ss_e2_series_panto;				
	}

switch (FEAT_TRAINS, SELF, sw_e2_series_forwards, num_vehs_in_consist ) {				
		0..23:  sw_e2_series_forwards_panto_in_middle;
		24..30: sw_e2_series_forwards_4th_and_6th;
        sw_e2_series_forwards_4th_and_5_from_end;				
	}

// Backwards
switch (FEAT_TRAINS, SELF, sw_e2_series_backwards_panto_in_middle, (position_in_consist == position_in_consist_from_end ) || (position_in_consist - 3 == position_in_consist_from_end)) {				
		0:  ss_e2_series_coach_rev;
		ss_e2_series_panto_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_e2_series_backwards_4th_and_6th, position_in_consist_from_end == 10 || position_in_consist_from_end == 16) {				
		0:  ss_e2_series_coach_rev;
		ss_e2_series_panto_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_e2_series_backwards_4th_and_5_from_end, position_in_consist_from_end == 10 || position_in_consist  == 13) {				
		0:  ss_e2_series_coach_rev;
		ss_e2_series_panto_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_e2_series_backwards, num_vehs_in_consist ) {				
		0..23:  sw_e2_series_backwards_panto_in_middle;
		24..30: sw_e2_series_backwards_4th_and_6th;
        sw_e2_series_backwards_4th_and_5_from_end;				
	}

// Is the Shinkansen reversed?
switch (FEAT_TRAINS, PARENT, sw_e2_series_midcars, vehicle_is_reversed) {				
		0:  sw_e2_series_forwards;
		sw_e2_series_backwards;				
	}

// Rear Car Selection
switch (FEAT_TRAINS, SELF, sw_e2_series_back, position_in_consist_from_end) {				
		1:  ss_e2_series_cab_rev;
		sw_e2_series_midcars;				
	}

// Front Cab selection
switch (FEAT_TRAINS, SELF, sw_e2_series_front, position_in_consist) {				
		1:  ss_e2_series_cab;
		sw_e2_series_back;				
	}

// GRAPHICS
switch (FEAT_TRAINS, SELF, sw_e2_series_graphics, position_in_consist % 3) {				
		1:  sw_e2_series_front; 				
		dummy_sprites;
	}

switch (FEAT_TRAINS, SELF, sw_e2_series_articulated_part, extra_callback_info1) {
		1 .. 2: return series_e2;									
		return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
	}

/*---Coupling Switch---*/
switch(FEAT_TRAINS, SELF, sw_e2_series_attach_vehid, [vehicle_type_id == mu_car_full]) {
 0: return string(ONLY_ADD_MUCAR);
}

/*---Length Check---*/
switch (FEAT_TRAINS, SELF, sw_e2_series_stop_start, num_vehs_in_consist < 12) {				
		1:  return string(STR_LENGTH_ERROR); 				
		return CB_RESULT_NO_TEXT; 
	}

item(FEAT_TRAINS, series_e2) {
	property {
		/* Menu */
        name: 							string(STR_NAME_E2_SERIES);
		sprite_id:                      SPRITE_ID_NEW_TRAIN; 		// required to enable new graphics
		misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
		running_cost_base:              RUNNING_COST_ELECTRIC;
		engine_class:                   ENGINE_CLASS_ELECTRIC;
        /* Availability */
        climates_available:             ALL_CLIMATES;
		introduction_date:              date(1997,03,22);
		model_life:                     VEHICLE_NEVER_EXPIRES;		//
		vehicle_life:                   30;							// 
		track_type:                     SHINKANSEN;
        /* Cargo */
        reliability_decay:              5;
		refittable_cargo_classes:       bitmask(CC_PASSENGERS);
		refit_cost:                     0;
		default_cargo_type:             PASS;
		ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;							// 
		loading_speed:                  50;							
		cargo_age_period:               278; 						// 1.5 times normal for this expensive long-distance train
		/* Stats */
        speed:                          275 km/h;				    // Operational Top Speed
		power:                          8050 kW;                    // Overwritten by callback					
		weight:                         648 tons;                   // Overwritten by callback					
		tractive_effort_coefficient:    0.3;
		air_drag_coefficient:			0;
	}
	graphics {
		/* Menu */
        additional_text:                string(STR_DESC_E2_SERIES);
        cargo_subtype_text:             sw_e2_series_year_dep_subtype_text;
        can_attach_wagon:				sw_e2_series_attach_vehid;
		start_stop:						sw_e2_series_stop_start;
		/* Graphics */
        default:                        sw_e2_series_graphics;
		purchase:                       sw_e2_series_buy;
		articulated_part:               sw_e2_series_articulated_part;
		visual_effect_and_powered:      sw_visual_effect_and_powered;
        /* Stats */
        length:                         sw_param_length;
        power:                          max((NumberCarsInConsist() - 2 ),0) * 402 * 4;
        weight:                        	NumberCarsInConsist() * 46;
		cargo_capacity:                 27 * CapacityFactor();										// 815 capacity / (10 cars * 3 parts) is approx 27						
		loading_speed:					27 * CapacityFactor() / param_loading_2D;					// Capacity divided by door loading factor
		cargo_age_period:				param_decay_express;
		cost_factor:                    144;
		running_cost_factor:            (290 + 120 * NumberCarsInConsist() ) * RunningCostFactor();
		purchase_running_cost_factor:   290 + 120 * NumberCarsInConsist();	
	}

	/*Multiple Unit Car*/
	
      livery_override(mu_car_full){
        default:                        sw_e2_series_graphics;
        cargo_capacity:                 27 * CapacityFactor();
        cargo_age_period:				param_decay_express;
        loading_speed:					27 * CapacityFactor() / param_loading_2D;
		articulated_part:				sw_mucar_full_articulated_part;
		length:							sw_param_length;
		//power:						// Apply power in graphics block to calculate power based on number of motorised cars correctly
		//weight:						// Apply weight to graphics block to calculate Max T.E. correctly
        //running_cost_factor:            sw_e2_series_running_costs;
      }
}

/* End E2 series */
