/* Begin e2 Series */

#include "src/trains/e2_series/e2_series_subtypes.pnml"
#include "src/trains/e2_series/e2_series_gfx.pnml"
#include "src/trains/e2_series/e2_series_liveries.pnml"

// Standard Panto Positions
switch (FEAT_TRAINS, SELF, sw_e2_series_backwards_standard, position_in_consist_from_end % 30) {				
		10:  ss_e2_series_panto_rev;
		16: ss_e2_series_panto_rev;
		ss_e2_series_coach_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_e2_series_forwards_standard, position_in_consist % 30) {				
		10:  ss_e2_series_panto;
		16: ss_e2_series_panto;
		ss_e2_series_coach;				
	}

// Special Panto Positions
switch (FEAT_TRAINS, SELF, sw_e2_series_backwards_special, position_in_consist_from_end % 12) {				
		7:  ss_e2_series_panto_rev;
		ss_e2_series_coach_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_e2_series_forwards_special, position_in_consist % 12) {				
		7:  ss_e2_series_panto;
		ss_e2_series_coach;				
	}

// Which Panto Positions should be selected
switch (FEAT_TRAINS, SELF, sw_e2_series_forwards, num_vehs_in_consist == 12) {				
		0:  sw_e2_series_forwards_standard;
		sw_e2_series_forwards_special;				
	}

switch (FEAT_TRAINS, SELF, sw_e2_series_backwards, num_vehs_in_consist == 12) {				
		0:  sw_e2_series_backwards_standard;
		sw_e2_series_backwards_special;				
	}


// Is the Shinkansen reversed?
switch (FEAT_TRAINS, PARENT, sw_e2_series_midcars, vehicle_is_reversed) {				
		0:  sw_e2_series_forwards;
		sw_e2_series_backwards;				
	}

// Rear Car Selection
switch (FEAT_TRAINS, SELF, sw_e2_series_back, position_in_consist_from_end) {				
		1:  ss_e2_series_cab_rev;
		sw_e2_series_midcars;				
	}

// Front Cab selection
switch (FEAT_TRAINS, SELF, sw_e2_series_front, position_in_consist) {				
		1:  ss_e2_series_cab;
		sw_e2_series_back;				
	}

// GRAPHICS
switch (FEAT_TRAINS, SELF, sw_e2_series_graphics, position_in_consist % 3) {				
		1:  sw_e2_series_front; 				
		dummy_sprites;
	}

switch (FEAT_TRAINS, SELF, sw_e2_series_articulated_part, extra_callback_info1) {
		1 .. 2: return series_e2;									
		return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
	}

/*---Coupling Switch---*/
switch(FEAT_TRAINS, SELF, sw_e2_series_attach_vehid, [vehicle_type_id == mu_car_full]) {
 0: return string(ONLY_ADD_MUCAR);
}

/*---Length Check---*/
switch (FEAT_TRAINS, SELF, sw_e2_series_stop_start, num_vehs_in_consist < 12) {				
		1:  return string(STR_LENGTH_ERROR); 				
		return CB_RESULT_NO_TEXT; 
	}

item(FEAT_TRAINS, series_e2) {
	property {
		/* Menu */
        name: 							string(STR_NAME_E2_SERIES);
		sprite_id:                      SPRITE_ID_NEW_TRAIN; 		// required to enable new graphics
		misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
		running_cost_base:              RUNNING_COST_ELECTRIC;
		engine_class:                   ENGINE_CLASS_ELECTRIC;
        /* Availability */
        climates_available:             ALL_CLIMATES;
		introduction_date:              date(1997,03,22);
		model_life:                     VEHICLE_NEVER_EXPIRES;		//
		vehicle_life:                   30;							// 
		track_type:                     SHINKANSEN;
        /* Cargo */
        reliability_decay:              5;
		refittable_cargo_classes:       bitmask(CC_PASSENGERS);
		refit_cost:                     0;
		default_cargo_type:             PASS;
		ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;							// 
		loading_speed:                  50;							
		cargo_age_period:               278; 						// 1.5 times normal for this expensive long-distance train
		/* Stats */
        speed:                          275 km/h;				    // Operational Top Speed
		power:                          8050 kW;                    // Overwritten by callback					
		weight:                         648 tons;                   // Overwritten by callback					
		tractive_effort_coefficient:    0.3;
		air_drag_coefficient:			0;
	}
	graphics {
		/* Menu */
        additional_text:                string(STR_DESC_E2_SERIES);
        cargo_subtype_text:             sw_e2_series_year_dep_subtype_text;
        can_attach_wagon:				sw_e2_series_attach_vehid;
		start_stop:						sw_e2_series_stop_start;
		/* Graphics */
        default:                        sw_e2_series_graphics;
		purchase:                       sw_e2_series_buy;
		articulated_part:               sw_e2_series_articulated_part;
		visual_effect_and_powered:      sw_visual_effect_and_powered;
        /* Stats */
        length:                         sw_length_2_8_2;
        power:                          max((NumberCarsInConsist() - 2 ),0) * 402 * 4;
        weight:                        	NumberCarsInConsist() * 46;
		cargo_capacity:                 27 * CapacityFactor();										// 815 capacity / (10 cars * 3 parts) is approx 27						
		loading_speed:					27 * CapacityFactor() / param_loading_2D;					// Capacity divided by door loading factor
		cargo_age_period:				param_decay_express;
		cost_factor:                    144;
		running_cost_factor:            (90 + 45 * NumberCarsInConsist() ) * RunningCostFactor();
		purchase_running_cost_factor:   90 + 45 * NumberCarsInConsist();	
	}

	/*Multiple Unit Car*/
	
      livery_override(mu_car_full){
        default:                        sw_e2_series_graphics;
        cargo_capacity:                 27 * CapacityFactor();
        cargo_age_period:				param_decay_express;
        loading_speed:					27 * CapacityFactor() / param_loading_2D;
		articulated_part:				sw_mucar_full_articulated_part;
		length:							sw_length_2_8_2;
		//power:						// Apply power in graphics block to calculate power based on number of motorised cars correctly
		//weight:						// Apply weight to graphics block to calculate Max T.E. correctly
        //running_cost_factor:            sw_e2_series_running_costs;
      }
}

/* End e2 series */
