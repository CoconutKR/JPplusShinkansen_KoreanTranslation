/* N700 STATS */

/* LENGTH */

    /* Make the Middle long sprite length 8 and the dummy sprites length 2*/
    switch (FEAT_TRAINS, SELF, sw_n700_series_length_standard, position_in_consist%3) {
        1: return 8;
        return 2;
    }

    switch (FEAT_TRAINS, SELF, sw_n700_series_shorten_8_car_vehicle, position_in_consist%6) {
        2: return 7;
        return 1;
    }

    switch (FEAT_TRAINS, SELF, sw_n700_series_length, cargo_subtype) {
        0: sw_n700_series_length_standard;      
        sw_n700_series_shorten_8_car_vehicle;
    }

/* CARGO CAPACITY

    JPS 1248/1560/1872/2184
    JPS 624/780/936/1092

        16-car  8-car
    IRL 1323    546
    RW  1344    576  
    LS  2016    864
    DC  2688    1152

    */

    // round(IRL capacity/48)
    switch(FEAT_TRAINS, SELF, sw_n700_series_param_cargo_capacity_16car, param_basecapacity) {
        0: return 28;       // 1x Real World Capacity
        2: return 56;       // 2x Double Real World Capacity
        return 42;          // 1.5x Long Sprite Adjusted Capacity 
    }
    // round(IRL capacity/48)
    switch(FEAT_TRAINS, SELF, sw_n700_series_param_cargo_capacity_08car, param_basecapacity) {
        0: return 12;       // 1x Real World Capacity
        2: return 24;       // 2x Double Real World Capacity
        return 18;          // 1.5x Long Sprite Adjusted Capacity 
    }

    // Capacity for each length
    switch(FEAT_TRAINS, SELF, sw_n700_series_cargo_capacity, cargo_subtype) {
        0: sw_n700_series_param_cargo_capacity_16car;     // Length 16 (12.0 tiles)
        sw_n700_series_param_cargo_capacity_08car;        // Length 8 (6.0 tiles)
    }

/* WEIGHT */

    switch(FEAT_TRAINS, SELF, sw_n700_series_weight, cargo_subtype) {
        0: return 715;     // Weight of a 16 car as stated on Wikipedia
        return 715/2;      // Half of the above for an 8 car
    }

/* POWER */

    switch(FEAT_TRAINS, SELF, sw_n700_series_power, cargo_subtype) {
        0: return 22900;    // Power of a 16 car as stated on Wikipedia
        return 11450;       // Half of the above for an 8 car
    }

/* REFIT COST */

    switch(FEAT_TRAINS, SELF, sw_n700_series_refit_cost_minus1, cargo_subtype) {
        1: return 7;       // Currently 'Original 8 car', Cost to move to 'Original 16 car'     
        return 0;
    }
    
    switch(FEAT_TRAINS, SELF, sw_n700_series_refit_cost_plus1, cargo_subtype) {
        0: return -6;       // Currently 'Original 16 car', Refund to move to 'Original 8 car'               
        return 0;           // Currently 'Original 8 car', moving to 'Original 8 car' 
    }

    switch(FEAT_TRAINS, SELF, sw_n700_series_refit_cost, var[0x10,8,0xFF] - cargo_subtype) {
        /* Refit cost based on difference between target cargo sybtype and current cargo subtype.
	    Refitting to fewer carriages refunds money, while adding carriages costs (more) money.    */    
        -2:				7;
	    -1:				sw_n700_series_refit_cost_minus1;
	    0:				0;
	    1:				sw_n700_series_refit_cost_plus1;
	    2:				-6;
	    return CB_FAILED;
    }
