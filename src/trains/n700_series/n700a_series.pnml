/* Begin N700A Series */

/* Purchase Menu */
    spriteset (ss_n700a_series_buy,	        				"src/trains/dummy/purchase.png") {template_purchase(0, 0)}
    alternative_sprites(ss_n700a_series_buy,     			ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/n700_series/12/buy_n700a.png") {template_purchase(0, 0)}

 /* EMU Parts */

    /* "CAB" */
    spriteset(ss_n700a_series_cab_12,             		   "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_n700a_series_cab_rev_12,             	   "src/trains/dummy/empty.png") {template_dummy(0,0)}
	spriteset(ss_n700a_series_cab_10,             		   "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_n700a_series_cab_rev_10,             	   "src/trains/dummy/empty.png") {template_dummy(0,0)}

    alternative_sprites(ss_n700a_series_cab_12,            	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/n700_series/12/n700a.png") {template_12_standard(1,1)}
    alternative_sprites(ss_n700a_series_cab_rev_12,        	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/n700_series/12/n700a.png") {template_12_reversed(1,41)}
	alternative_sprites(ss_n700a_series_cab_10,            	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/n700_series/10/n700a.png") {template_10_standard(1,1)}
    alternative_sprites(ss_n700a_series_cab_rev_10,        	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/n700_series/10/n700a.png") {template_10_reversed(1,41)}

		// Parameter  length selection
		switch (FEAT_TRAINS, SELF, ss_n700a_series_cab, param_spritelength) {
			1: ss_n700a_series_cab_10;						// Length 10
			ss_n700a_series_cab_12;						    // Length 12
		}
		
        switch (FEAT_TRAINS, SELF, ss_n700a_series_cab_rev, param_spritelength) {
			1: ss_n700a_series_cab_rev_10;					// Length 10
			ss_n700a_series_cab_rev_12;						// Length 12
		}

    /* COACH */
    spriteset(ss_n700a_series_coach_12,             	    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_n700a_series_coach_rev_12,    		    	"src/trains/dummy/empty.png") {template_dummy(0,0)}
	spriteset(ss_n700a_series_coach_10,             	    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_n700a_series_coach_rev_10,    		    	"src/trains/dummy/empty.png") {template_dummy(0,0)}

    alternative_sprites(ss_n700a_series_coach_12,          	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/n700_series/12/n700a.png") {template_12_standard(1,121)}
    alternative_sprites(ss_n700a_series_coach_rev_12,      	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/n700_series/12/n700a.png") {template_12_reversed(1,121)}
	alternative_sprites(ss_n700a_series_coach_10,          	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/n700_series/10/n700a.png") {template_10_standard(1,121)}
    alternative_sprites(ss_n700a_series_coach_rev_10,      	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/n700_series/10/n700a.png") {template_10_reversed(1,121)}

		// Parameter  length selection
		switch (FEAT_TRAINS, SELF, ss_n700a_series_coach, param_spritelength) {
			1: ss_n700a_series_coach_10;					// Length 10
			ss_n700a_series_coach_12;						// Length 12
		}
		
        switch (FEAT_TRAINS, SELF, ss_n700a_series_coach_rev, param_spritelength) {
			1: ss_n700a_series_coach_rev_10;				// Length 10
			ss_n700a_series_coach_rev_12;					// Length 12
		}

    /* PANTO */
    spriteset(ss_n700a_series_panto_12,          		    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_n700a_series_panto_rev_12, 		        "src/trains/dummy/empty.png") {template_dummy(0,0)}
	spriteset(ss_n700a_series_panto_10,          		    "src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_n700a_series_panto_rev_10, 		        "src/trains/dummy/empty.png") {template_dummy(0,0)}

    alternative_sprites(ss_n700a_series_panto_12,     	    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,  "src/trains/n700_series/12/n700a.png") {template_12_standard(1,81)}
    alternative_sprites(ss_n700a_series_panto_rev_12,      	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,  "src/trains/n700_series/12/n700a.png") {template_12_reversed(1,81)}
	alternative_sprites(ss_n700a_series_panto_10,     	    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,  "src/trains/n700_series/10/n700a.png") {template_10_standard(1,81)}
    alternative_sprites(ss_n700a_series_panto_rev_10,      	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,  "src/trains/n700_series/10/n700a.png") {template_10_reversed(1,81)}

		// Parameter  length selection
		switch (FEAT_TRAINS, SELF, ss_n700a_series_panto, param_spritelength) {
			1: ss_n700a_series_panto_10;					// Length 10
			ss_n700a_series_panto_12;						// Length 12
		}
		
        switch (FEAT_TRAINS, SELF, ss_n700a_series_panto_rev, param_spritelength) {
			1: ss_n700a_series_panto_rev_10;				// Length 10
			ss_n700a_series_panto_rev_12;					// Length 12
		}

/* FORMATIONS */

// Forwards
switch (FEAT_TRAINS, SELF, sw_n700a_series_forwards_5th_and_4_from_end, position_in_consist == 13 || position_in_consist_from_end  == 10) {				
		0:  ss_n700a_series_coach;
		ss_n700a_series_panto;				
	}

switch (FEAT_TRAINS, SELF, sw_n700a_series_forwards_2nd_and_penultimate, position_in_consist == 4 || position_in_consist_from_end  == 4) {				
		0:  ss_n700a_series_coach;
		ss_n700a_series_panto;				
	}

switch (FEAT_TRAINS, SELF, sw_n700a_series_forwards_panto_in_middle, (position_in_consist == position_in_consist_from_end ) || (position_in_consist - 3 == position_in_consist_from_end)) {				
		0:  ss_n700a_series_coach;
		ss_n700a_series_panto;				
	}

switch (FEAT_TRAINS, SELF, sw_n700a_series_forwards, num_vehs_in_consist ) {				
		0..21:  sw_n700a_series_forwards_panto_in_middle;
		22..24: sw_n700a_series_forwards_2nd_and_penultimate;
        25..35: sw_n700a_series_forwards_panto_in_middle;
        sw_n700a_series_forwards_5th_and_4_from_end;
	}

// Backwards
switch (FEAT_TRAINS, SELF, sw_n700a_series_backwards_5th_and_4_from_end, position_in_consist == 10 || position_in_consist_from_end  == 13) {				
		0:  ss_n700a_series_coach_rev;
		ss_n700a_series_panto_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_n700a_series_backwards_2nd_and_penultimate, position_in_consist == 4 || position_in_consist_from_end  == 4) {				
		0:  ss_n700a_series_coach_rev;
		ss_n700a_series_panto_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_n700a_series_backwards_panto_in_middle, (position_in_consist == position_in_consist_from_end ) || (position_in_consist - 3 == position_in_consist_from_end)) {				
		0:  ss_n700a_series_coach_rev;
		ss_n700a_series_panto_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_n700a_series_backwards, num_vehs_in_consist ) {				
		0..21:  sw_n700a_series_backwards_panto_in_middle;
		22..24: sw_n700a_series_backwards_2nd_and_penultimate;
        25..35: sw_n700a_series_backwards_panto_in_middle;
        sw_n700a_series_backwards_5th_and_4_from_end;
	}

switch (FEAT_TRAINS, PARENT, sw_n700a_series_midcars, vehicle_is_reversed) {				
		0:  sw_n700a_series_forwards;
		sw_n700a_series_backwards;				
	}

switch (FEAT_TRAINS, SELF, sw_n700a_series_back, position_in_consist_from_end) {				
		1:  ss_n700a_series_cab_rev;
		sw_n700a_series_midcars;				
	}

switch (FEAT_TRAINS, SELF, sw_n700a_series_front, position_in_consist) {				
		1:  ss_n700a_series_cab;
		sw_n700a_series_back;				
	}

switch (FEAT_TRAINS, SELF, sw_n700a_series_graphics, position_in_consist % 3) {				
		1:  sw_n700a_series_front; 				
		dummy_sprites;
	}

switch (FEAT_TRAINS, SELF, sw_n700a_series_articulated_part, extra_callback_info1) {
		1 .. 2: return series_n700a;									
		return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
	}

/*---Coupling Switch---*/
switch(FEAT_TRAINS, SELF, sw_n700a_series_attach_vehid, [vehicle_type_id == mu_car_full]) {
 0: return string(ONLY_ADD_MUCAR);
}

/*---Length Check---*/
switch (FEAT_TRAINS, SELF, sw_n700a_series_stop_start, num_vehs_in_consist < 12) {				
		1:  return string(STR_LENGTH_ERROR); 				
		return CB_RESULT_NO_TEXT; 
	}

item(FEAT_TRAINS, series_n700a) {
	property {
		/* Menu */
        name: 							string(STR_NAME_N700A_SERIES);
        sprite_id:                      SPRITE_ID_NEW_TRAIN; 		// required to enable new graphics
		misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
        running_cost_base:              RUNNING_COST_ELECTRIC;
		engine_class:                   ENGINE_CLASS_ELECTRIC;
		variant_group:					707;
        /* Availability */
        climates_available:             ALL_CLIMATES;
		introduction_date:              date(2013,02,08);
		model_life:                     VEHICLE_NEVER_EXPIRES;		//
		vehicle_life:                   30;							// 
		track_type:                     SHINKANSEN;
		extra_flags:					bitmask(VEHICLE_FLAG_DISABLE_EXCLUSIVE_PREVIEW);
        /* Cargo */
        reliability_decay:              5;
		refittable_cargo_classes:       bitmask(CC_PASSENGERS);
		refit_cost:                     0;
		default_cargo_type:             PASS;
		ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;							// 
		loading_speed:                  50;							
		cargo_age_period:               278; 						// Default 185 x 1.5
		/* Stats */
		speed:                          301 km/h;				    // Overwritten by callback
    	power:                          22900 kW;					// Overwritten by callback
		weight:                         648 tons;					// Overwritten by callback
		tractive_effort_coefficient:    0.3;

	}
	graphics {
		/* Menu */
        additional_text:                string(STR_DESC_N700A_SERIES);
		can_attach_wagon:				sw_n700a_series_attach_vehid;
		start_stop:						sw_n700a_series_stop_start;
		purchase_weight:                44;
        purchase_power:                 409 * 4;
        /* Graphics */
        default:                        sw_n700a_series_graphics;
		purchase:                       ss_n700a_series_buy;
		articulated_part:               sw_n700a_series_articulated_part;
		visual_effect_and_powered:      sw_visual_effect_and_powered;
        /* Stats */
        length:                         sw_param_length;
        weight:                         NumberCarsInConsist() * 44;
        power:                         	max (( NumberCarsInConsist() - 2 ) , 0 )* 409 * 4;
		cargo_capacity:                 28 * CapacityFactor();															
		loading_speed:					28 * CapacityFactor() / param_loading_2D;					// Capacity divided by door loading factor
		cost_factor:                    138;
		running_cost_factor:            (300 + 125 * NumberCarsInConsist() ) * RunningCostFactor();
		purchase_running_cost_factor:   300 + 125 * NumberCarsInConsist();
	}
	/*Multiple Unit Car*/
	
      livery_override(mu_car_full){
        default:                        sw_n700a_series_graphics;
        cargo_capacity:                 28 * CapacityFactor();	
        cargo_age_period:				param_decay_express;
        loading_speed:                  28 * CapacityFactor() / param_loading_2D;
		articulated_part:				sw_mucar_full_articulated_part;
		length:							sw_param_length;
		//power:						// Apply power in graphics block to calculate power based on number of motorised cars correctly
		//weight:						// Apply weight to graphics block to calculate Max T.E. correctly
        //running_cost_factor:            sw_n700a_series_running_costs;
      }
}

/* End N700 series */
