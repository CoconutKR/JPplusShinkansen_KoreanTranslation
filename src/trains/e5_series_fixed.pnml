/* Begin e5 Series */

/* SPRITES */

/* Purchase Menu */
spriteset (ss_e5_series_purchase_pink_livery,	        	"src/gfx/dummy/purchase.png") {template_purchase(0, 0)}
spriteset (ss_e5_series_purchase_purple_livery,	        	"src/gfx/dummy/purchase.png") {template_purchase(0, 0)}

alternative_sprites(ss_e5_series_purchase_pink_livery,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/gfx/e5_series/purchase.png") {template_purchase(0, 0)}
alternative_sprites(ss_e5_series_purchase_purple_livery,    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/gfx/e5_series/purchase.png") {template_purchase(0, 0)}

/* EMU Parts */

/* Cab  */
spriteset(ss_e5_series_cab_pink_livery,             		"src/gfx/dummy/empty.png") {template_dummy(0,0)}
spriteset(ss_e5_series_cab_rev_pink_livery,             	"src/gfx/dummy/empty.png") {template_dummy(0,0)}
spriteset(ss_e5_series_cab_purple_livery,             		"src/gfx/dummy/empty.png") {template_dummy(0,0)}
spriteset(ss_e5_series_cab_rev_purple_livery,             	"src/gfx/dummy/empty.png") {template_dummy(0,0)}


// E5 Series Pink Strip Livery
alternative_sprites(ss_e5_series_cab_pink_livery,     		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/gfx/e5_series/e5pink.png")  {template_12_standard(1,1)}
alternative_sprites(ss_e5_series_cab_rev_pink_livery,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/gfx/e5_series/e5pink.png")  {template_12_reversed(1,41)}			

// E5 Series Purple Strip Livery
alternative_sprites(ss_e5_series_cab_purple_livery,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,  "src/gfx/e5_series/h5purple.png") {template_12_standard(1,1)}
alternative_sprites(ss_e5_series_cab_rev_purple_livery,    	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,  "src/gfx/e5_series/h5purple.png") {template_12_reversed(1,41)}

// Coach
spriteset(ss_e5_series_coach_pink_livery,             		"src/gfx/dummy/empty.png") {template_dummy(0,0)}
spriteset(ss_e5_series_coach_rev_pink_livery,    			"src/gfx/dummy/empty.png") {template_dummy(0,0)}
spriteset(ss_e5_series_coach_purple_livery,             	"src/gfx/dummy/empty.png") {template_dummy(0,0)}
spriteset(ss_e5_series_coach_rev_purple_livery,    			"src/gfx/dummy/empty.png") {template_dummy(0,0)}

// E5 Series Pink Strip Livery
alternative_sprites(ss_e5_series_coach_pink_livery,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/gfx/e5_series/e5pink.png") {template_12_standard(1,121)}
alternative_sprites(ss_e5_series_coach_rev_pink_livery,		ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/gfx/e5_series/e5pink.png") {template_12_reversed(1,121)}

// E5 Series Purple Strip Livery
alternative_sprites(ss_e5_series_coach_purple_livery,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/gfx/e5_series/h5purple.png") {template_12_standard(1,121)}
alternative_sprites(ss_e5_series_coach_rev_purple_livery,  	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/gfx/e5_series/h5purple.png") {template_12_reversed(1,121)}	

// Coach Panto
spriteset(ss_e5_series_panto_pink_livery,          			"src/gfx/dummy/empty.png")     						{template_dummy(0,0)}
spriteset(ss_e5_series_panto_rev_pink_livery, 				"src/gfx/dummy/empty.png")     						{template_dummy(0,0)}
spriteset(ss_e5_series_panto_purple_livery,          		"src/gfx/dummy/empty.png")     						{template_dummy(0,0)}
spriteset(ss_e5_series_panto_rev_purple_livery, 		    "src/gfx/dummy/empty.png")     						{template_dummy(0,0)}

// E5 Series pink Strip Livery
alternative_sprites(ss_e5_series_panto_pink_livery,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/gfx/e5_series/e5pink.png") {template_12_standard(1,81)}
alternative_sprites(ss_e5_series_panto_rev_pink_livery, 	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/gfx/e5_series/e5pink.png") {template_12_reversed(1,81)}	

// E5 Series purple Strip Livery
alternative_sprites(ss_e5_series_panto_purple_livery,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/gfx/e5_series/h5purple.png") {template_12_standard(1,81)}
alternative_sprites(ss_e5_series_panto_rev_purple_livery,  	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/gfx/e5_series/h5purple.png") {template_12_reversed(1,81)}	

/* PICK THE LIVERY */

// CAB

switch(FEAT_TRAINS, SELF, sw_e5_series_cab_new_livery, cargo_subtype) {
    1: ss_e5_series_cab_purple_livery;
    ss_e5_series_cab_pink_livery;
}

switch(FEAT_TRAINS, SELF, ss_e5_series_cab, current_year) {
    0..2016: ss_e5_series_cab_pink_livery;
    sw_e5_series_cab_new_livery;
}

// COACH

switch(FEAT_TRAINS, SELF, sw_e5_series_coach_new_livery, cargo_subtype) {
    1: ss_e5_series_coach_purple_livery;
    ss_e5_series_coach_pink_livery;
}

switch(FEAT_TRAINS, SELF, ss_e5_series_coach, current_year) {
    0..2016: ss_e5_series_coach_pink_livery;
    sw_e5_series_coach_new_livery;
}

// Coach Reversed

switch(FEAT_TRAINS, SELF, sw_e5_series_coach_rev_new_livery, cargo_subtype) {
    1: ss_e5_series_coach_rev_purple_livery;
    ss_e5_series_coach_rev_pink_livery;
}

switch(FEAT_TRAINS, SELF, ss_e5_series_coach_rev, current_year) {
    0..2016: ss_e5_series_coach_rev_pink_livery;
    sw_e5_series_coach_rev_new_livery;
}

// Panto

switch(FEAT_TRAINS, SELF, sw_e5_series_panto_new_livery, cargo_subtype) { 
    1: ss_e5_series_panto_purple_livery;
    ss_e5_series_panto_pink_livery;
}

switch(FEAT_TRAINS, SELF, ss_e5_series_panto, current_year) {
    0..2016: ss_e5_series_panto_pink_livery;
    sw_e5_series_panto_new_livery;
}

// PANTO REVERSED

switch(FEAT_TRAINS, SELF, sw_e5_series_panto_rev_new_livery, cargo_subtype) {
    1: ss_e5_series_panto_rev_purple_livery;
    ss_e5_series_panto_rev_pink_livery;
}

switch(FEAT_TRAINS, SELF, ss_e5_series_panto_rev, current_year) {
    0..2016: ss_e5_series_panto_rev_pink_livery;
    sw_e5_series_panto_rev_new_livery;
}

// CAB REVERSED

switch(FEAT_TRAINS, SELF, sw_e5_series_cab_rev_new_livery, cargo_subtype) {
    1: ss_e5_series_cab_rev_purple_livery;
    ss_e5_series_cab_rev_pink_livery;
}

switch(FEAT_TRAINS, SELF, ss_e5_series_cab_rev, current_year) {
    0..2016: ss_e5_series_cab_rev_pink_livery;
    sw_e5_series_cab_rev_new_livery;
}

// Pre 2016 subtype text
switch(FEAT_TRAINS, SELF, sw_e5_series_old_livery_subtype_text, cargo_subtype) {
	0: return string(LV_E5_SERIES_E5); 
       return CB_RESULT_NO_TEXT;
    }

// Post 2016 subtype text
switch(FEAT_TRAINS, SELF, sw_e5_series_new_livery_subtype_text, cargo_subtype) {
	0: return string(LV_E5_SERIES_E5);
    1: return string(LV_E5_SERIES_H5);
       return CB_RESULT_NO_TEXT;
    }

switch(FEAT_TRAINS, SELF, sw_e5_series_year_dep_subtype_text, current_year) {
    0..2016: sw_e5_series_old_livery_subtype_text;
    sw_e5_series_new_livery_subtype_text;
}

/* Switches to set which sprite to show going forwards vs going backwards */

// Coach Car going forwards and a Panto Car going backwards
switch (FEAT_TRAINS, PARENT, sw_e5_series_reverser_C_P, vehicle_is_reversed) {
	0: ss_e5_series_coach;
	ss_e5_series_panto_rev;
}

// Coach Car going forwards and a Coach Car going backwards
switch (FEAT_TRAINS, PARENT, sw_e5_series_reverser_C_C, vehicle_is_reversed) {
	0: ss_e5_series_coach;
	ss_e5_series_coach_rev;
}

// Panto Car going forwards and a Coach Car going backwards
switch (FEAT_TRAINS, PARENT, sw_e5_series_reverser_P_C, vehicle_is_reversed) {
	0: ss_e5_series_panto;
	ss_e5_series_coach_rev;
}

/* LENGTH*/

/* Make the Middle long sprite length 8 and the dummy sprites length 2*/
switch (FEAT_TRAINS, SELF, sw_e5_series_length, position_in_consist%3) {
    1: return 8;
    return 2;
}

/* PUT TOGETHER THE EMU*/

/* Select the correct part of the train */
switch (FEAT_TRAINS, SELF, sw_e5_series_graphics, position_in_articulated_veh) {				
	1:  ss_e5_series_cab; 				// Car 1								
	4:  sw_e5_series_reverser_C_C;		// Car 2									
	7:  sw_e5_series_reverser_P_C;		// Car 3
	10: sw_e5_series_reverser_C_P;		// Car 4
    13: sw_e5_series_reverser_C_C;		// Car 5
    16: sw_e5_series_reverser_C_C;		// Car 6
    19: sw_e5_series_reverser_P_C;		// Car 7
	22: sw_e5_series_reverser_C_P;		// Car 8	
    25: sw_e5_series_reverser_C_C;		// Car 9
	28: ss_e5_series_cab_rev;			// Car 10
	dummy_sprites;
}

/* Assemble an 10-car articulated vehicle. */
switch (FEAT_TRAINS, SELF, sw_e5_series_articulated_part, extra_callback_info1) {
	1 .. 29: return series_e5;										// Should be same as range above
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, sw_e5_series_visual_effect_and_powered, ((position_in_articulated_veh == 0) && !func_is_reversed()) || ((position_in_articulated_veh_from_end == 0) && func_is_reversed()) ) {
	1: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 0, ENABLE_WAGON_POWER);
	return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

item(FEAT_TRAINS, series_e5, 2005) {
	property {
		name: 							string(STR_NAME_E5_SERIES);
		climates_available:             ALL_CLIMATES;
		introduction_date:              date(2011,03,05); 
		model_life:                     VEHICLE_NEVER_EXPIRES;		
		vehicle_life:                   30;							// 
		reliability_decay:              5;
		refittable_cargo_classes:       bitmask(CC_PASSENGERS);
		refit_cost:                     0;
		default_cargo_type:             PASS;
		cargo_capacity:                 1;							// 
		loading_speed:                  50;							
		cargo_age_period:               278; 						// 1.5 times normal for this expensive long-distance train
		sprite_id:                      SPRITE_ID_NEW_TRAIN; 		// required to enable new graphics
		speed:                          321 km/h;					// Operational Top Speed
		misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
		track_type:                     SHINKANSEN;
		ai_special_flag:                AI_FLAG_PASSENGER;
		running_cost_base:              RUNNING_COST_STEAM;
		engine_class:                   ENGINE_CLASS_ELECTRIC;
		power:                          20380 kW;						// 7.5 tile JapanSet E5
		weight:                         675 tons;					// 45t x 10cars x 1.5longsprite
		tractive_effort_coefficient:    0.3;

	}
	graphics {
		default:                        sw_e5_series_graphics;
		purchase:                       ss_e5_series_purchase_pink_livery;
		articulated_part:               sw_e5_series_articulated_part;
		length:                         sw_e5_series_length;
		cargo_capacity:                 50;
		//colour_mapping:                 sw_cc_blue_white;s
		cargo_subtype_text:             sw_e5_series_year_dep_subtype_text;
		cost_factor:                    257;
		running_cost_factor:            func_running_costs(300);
		purchase_running_cost_factor:   300;
		visual_effect_and_powered:      sw_e5_series_visual_effect_and_powered;
		additional_text:                string(STR_DESC_E5_SERIES);
	}
}

/* End e5 series */
