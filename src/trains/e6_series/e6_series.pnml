// Begin e6_series

/* GRAPHICS */

// Purchase Sprites
  spriteset (spriteset_e6_series_purchase,	        	  "src/trains/dummy/purchase.png") {tmpl_purchase_dh(0, 0)}
  alternative_sprites(spriteset_e6_series_purchase,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e6_series/purchase.png") {tmpl_purchase_dh_32bpp(0, 0)}
    
// Cab 
  spriteset(ss_e6_series_cabyf,                       "src/trains/dummy/mucar.png") {template_8_standard(1,1)}
  spriteset(ss_e6_series_cabrb,                       "src/trains/dummy/mucar.png") {template_8_standard(1,1)}
  spriteset(ss_e6_series_cabyb,                       "src/trains/dummy/mucar.png") {template_8_standard(1,1)}
  spriteset(ss_e6_series_cabrf,                       "src/trains/dummy/mucar.png") {template_8_standard(1,1)}
  
  alternative_sprites(ss_e6_series_cabyf,             ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e6_series/e6.png") {template_8_standard(1,1)}
  alternative_sprites(ss_e6_series_cabrb,             ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e6_series/e6.png") {template_8_standard(1,41)}
  alternative_sprites(ss_e6_series_cabyb,             ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e6_series/e6.png") {template_8_standard(1,81)}
  alternative_sprites(ss_e6_series_cabrf,             ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e6_series/e6.png") {template_8_standard(1,121)}

// Coach
  spriteset(ss_e6_series_coach,                       "src/trains/dummy/mucar.png") {template_8_standard(1,1)}
  spriteset(ss_e6_series_coach_rev,                   "src/trains/dummy/mucar.png") {template_8_standard(1,1)}
  
  alternative_sprites(ss_e6_series_coach,             ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e6_series/e6.png") {template_8_standard(1,201)}
  alternative_sprites(ss_e6_series_coach_rev,         ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e6_series/e6.png") {template_8_reversed(1,201)}

// Pant2
  spriteset(ss_e6_series_pant2,                       "src/trains/dummy/mucar.png") {template_8_standard(1,1)}
  spriteset(ss_e6_series_pant2_rev,                   "src/trains/dummy/mucar.png") {template_8_standard(1,1)}
  
  alternative_sprites(ss_e6_series_pant2,             ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e6_series/e6.png") {template_8_standard(1,161)}
  alternative_sprites(ss_e6_series_pant2_rev,         ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e6_series/e6.png") {template_8_reversed(1,161)}

// Pant6
  spriteset(ss_e6_series_pant6,                       "src/trains/dummy/mucar.png") {template_8_standard(1,1)}
  spriteset(ss_e6_series_pant6_rev,                   "src/trains/dummy/mucar.png") {template_8_standard(1,1)}
  
  alternative_sprites(ss_e6_series_pant6,             ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e6_series/e6.png") {template_8_standard(1,241)}
  alternative_sprites(ss_e6_series_pant6_rev,         ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/e6_series/e6.png") {template_8_reversed(1,241)}


    // Midcars
    switch (FEAT_TRAINS, PARENT, sw_e6_series_midcars, vehicle_is_reversed ) {				
        0:  ss_e6_series_coach;
        ss_e6_series_coach_rev;			
      }

    // Standard
    switch (FEAT_TRAINS, SELF, sw_e6_series_2nd_car_standard_backwards, position_in_consist_from_end == 1 ) {				
        1:  ss_e6_series_pant2_rev;
        ss_e6_series_pant6_rev;			
      }

    switch (FEAT_TRAINS, SELF, sw_e6_series_2nd_car_standard_forwards, position_in_consist == 1 ) {				
        1:  ss_e6_series_pant2;
        ss_e6_series_pant6;			
      }

    switch (FEAT_TRAINS, PARENT, sw_e6_series_2nd_car_standard, vehicle_is_reversed ) {				
        0:  sw_e6_series_2nd_car_standard_forwards;
        sw_e6_series_2nd_car_standard_backwards;			
      }

    // Short
    switch (FEAT_TRAINS, SELF, sw_e6_series_2nd_car_short_backwards, position_in_consist ) {				
        1:  ss_e6_series_pant6_rev;
        ss_e6_series_coach_rev;			
      }

    switch (FEAT_TRAINS, SELF, sw_e6_series_2nd_car_short_forwards, position_in_consist_from_end ) {				
        1:  ss_e6_series_pant6;
        ss_e6_series_coach;			
      }

    switch (FEAT_TRAINS, PARENT, sw_e6_series_2nd_car_short, vehicle_is_reversed ) {				
        0:  sw_e6_series_2nd_car_short_forwards;
        sw_e6_series_2nd_car_short_backwards;			
      }

    // Short or Standard?
    switch (FEAT_TRAINS, SELF, sw_e6_series_2nd_car_length_check, num_vehs_in_consist < 7 ) {				
        1:  sw_e6_series_2nd_car_short;
        sw_e6_series_2nd_car_standard;			
      }

    // Identify the 2nd cars
    switch (FEAT_TRAINS, SELF, sw_e6_series_2nd_car_checks, position_in_consist == 1 || position_in_consist_from_end == 1 ) {				
        1:  sw_e6_series_2nd_car_length_check;
        sw_e6_series_midcars;			
      }

    // First car checks
    switch (FEAT_TRAINS, SELF, sw_e6_series_cab_not_reversed, position_in_consist) {				
        0:  ss_e6_series_cabyf;     
        ss_e6_series_cabrf;			
      }

    switch (FEAT_TRAINS, SELF, sw_e6_series_cab_reversed, position_in_consist){				
        0:  ss_e6_series_cabyb;     
        ss_e6_series_cabrb;		  
      }

    switch (FEAT_TRAINS, PARENT, sw_e6_series_front_cab_checks, vehicle_is_reversed) {				
        1:  sw_e6_series_cab_reversed;
        sw_e6_series_cab_not_reversed;				
      }

    // Is it a cab car?
    switch (FEAT_TRAINS, SELF, sw_e6_series_graphics, position_in_consist == 0 || position_in_consist_from_end == 0) {				
        1:  sw_e6_series_front_cab_checks;
        sw_e6_series_2nd_car_checks;				
      }

/*---Coupling Switch---*/
switch(FEAT_TRAINS, SELF, sw_e6_series_attach_vehid, [vehicle_type_id == series_e6 || vehicle_type_id == mu_car_ms]) {
 0: return string(MU_UNIQUE);
}
/*---END OF SECTION---*/

item(FEAT_TRAINS, series_e6, 2006) {
    property {
        name:                           string(STR_NAME_E6_SERIES);
        introduction_date:              date(2013,03,16);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   24;
        reliability_decay:              23;

        cargo_capacity:                 50;
        loading_speed:                  20;
        cost_factor:                    175;
        running_cost_factor:            120;

        speed:                          321 km/h;
        track_type:                     MINI_SHINKANSEN;
        power:                          400 * 4 hp;
        weight:                         42 ton;

        climates_available:             ALL_CLIMATES;
        ai_special_flag:                AI_FLAG_PASSENGER;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   bitmask();
        refit_cost:                     0;
        //dual_headed:                    1;
        ai_engine_rank:                 0;

        running_cost_base:              RUNNING_COST_ELECTRIC;
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 4, ENABLE_WAGON_POWER);

        tractive_effort_coefficient:    0.3;
        air_drag_coefficient:           0;
        bitmask_vehicle_info:           0;
        length:                         8;
    }
    /*Graphics and Callbacks*/
      graphics {
        default:                        sw_e6_series_graphics;
		    purchase:                       spriteset_e6_series_purchase;
		    additional_text:	              return string(STR_DESC_E6_SERIES);
        //cargo_subtype_text:           sw_e6_series_subtypetext_main;
        can_attach_wagon:			          sw_e6_series_attach_vehid;
        //start_stop:                   ;
        purchase_weight:                42 / 2;
        purchase_power:                 400 * 4;
        /* Stats */
        power:                          max (num_vehs_in_consist - 2 , 0 ) * 400 * 4;
        cargo_capacity:                 48 * CapacityFactor();										        // 338 capacity / 7 cars  is approx 48						
		    loading_speed:					        48 * CapacityFactor() / param_loading_1D;					// Capacity divided by door loading factor
        cargo_age_period:				        param_decay_express;
        running_cost_factor:            (450 + 127 * num_vehs_in_consist ) * RunningCostFactor();
        purchase_running_cost_factor:   (450 + 127) / 2;
    }
    /*Multiple Unit Car*/
      livery_override(mu_car_ms){
        default:                        sw_e6_series_graphics;
        cargo_capacity:                 48 * CapacityFactor();
        cargo_age_period:				        param_decay_express;
        loading_speed:                  48 * CapacityFactor() / param_loading_1D;
        //cargo_subtype_text:           sw_e6_series_subtypetext_mucar;
        //power:                        // Apply power in graphics block to calculate power based on number of motorised cars correctly
        weight:                         42;
        //running_cost_factor:            120;
      }

}

// End e6_series
