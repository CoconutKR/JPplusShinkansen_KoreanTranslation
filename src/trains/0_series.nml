/* Begin 0 Series */

/* SPRITES */

/* Lights */
spriteset(spriteset_0_series_front_headlights,  		"src/gfx/0_series/front/1x_headlights.png")     	{template_6_standard(0,0)}
spriteset(spriteset_0_series_front_rearlights,			"src/gfx/0_series/front/1x_rearlights.png")    		{template_6_reversed(0,0)}
spriteset(spriteset_0_series_back_headlights,   		"src/gfx/0_series/back/1x_headlights.png")     		{template_6_reversed(0,0)}
spriteset(spriteset_0_series_back_rearlights,   		"src/gfx/0_series/back/1x_rearlights.png")     		{template_6_standard(0,0)}

/* EMU Parts */

// Front Cab
spriteset(spriteset_0_series_front_1,             		"src/gfx/0_series/front/1x_8bpp.png")     			{template_6_standard(0,0)}
spriteset(spriteset_0_series_front_reversed_1,    		"src/gfx/0_series/front/1x_8bpp.png")        		{template_6_reversed(0,0)}

spriteset(spriteset_0_series_front_2,             		"src/gfx/0_series/front/1x_8bpp.png")     			{template_6_standard(0,0)}
spriteset(spriteset_0_series_front_reversed_2,    		"src/gfx/0_series/front/1x_8bpp.png")        		{template_6_reversed(0,0)}

alternative_sprites(spriteset_0_series_front_2,     		
	ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP,     				"src/gfx/0_series/front/2x_8bpp.png")     	 		{template_2x_6_standard(0,0)}
alternative_sprites(spriteset_0_series_front_reversed_2,  
	ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP,     				"src/gfx/0_series/front/2x_8bpp.png")      			{template_2x_6_reversed(0,0)}

// Coach-front
spriteset(spriteset_0_series_coach_front_1,             "src/gfx/0_series/coach_front/1x_8bpp.png")    		{template_6_standard(0,0)}
spriteset(spriteset_0_series_coach_front_reversed_1,    "src/gfx/0_series/coach_front/1x_8bpp.png")    		{template_6_reversed(0,0)}

spriteset(spriteset_0_series_coach_front_2,             "src/gfx/0_series/coach_front/1x_8bpp.png")    		{template_6_standard(0,0)}
spriteset(spriteset_0_series_coach_front_reversed_2,    "src/gfx/0_series/coach_front/1x_8bpp.png")    		{template_6_reversed(0,0)}

alternative_sprites(spriteset_0_series_coach_front_2,     		
	ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP,     				"src/gfx/0_series/coach_front/2x_8bpp.png")   		{template_2x_6_standard(0,0)}

alternative_sprites(spriteset_0_series_coach_front_reversed_2,  
	ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP,     				"src/gfx/0_series/coach_front/2x_8bpp.png")  		{template_2x_6_reversed(0,0)}

// Coach-back
spriteset(spriteset_0_series_coach_back_1,           	"src/gfx/0_series/coach_back/1x_8bpp.png")      	{template_6_standard(0,0)}
spriteset(spriteset_0_series_coach_back_reversed_1,  	"src/gfx/0_series/coach_back/1x_8bpp.png")     		{template_6_reversed(0,0)}

spriteset(spriteset_0_series_coach_back_2,           	"src/gfx/0_series/coach_back/1x_8bpp.png")      	{template_6_standard(0,0)}
spriteset(spriteset_0_series_coach_back_reversed_2,  	"src/gfx/0_series/coach_back/1x_8bpp.png")     		{template_6_reversed(0,0)}

alternative_sprites(spriteset_0_series_coach_back_2,     		
	ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP,     				"src/gfx/0_series/coach_back/2x_8bpp.png")   		{template_2x_6_standard(0,0)}

alternative_sprites(spriteset_0_series_coach_back_reversed_2,  
	ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP,     				"src/gfx/0_series/coach_back/2x_8bpp.png")  		{template_2x_6_reversed(0,0)}

// Coach-back-panto
spriteset(spriteset_0_series_coach_back_panto_1,          "src/gfx/0_series/coach_back_panto/1x_8bpp.png")	{template_6_standard(0,0)}
spriteset(spriteset_0_series_coach_back_panto_reversed_1, "src/gfx/0_series/coach_back_panto/1x_8bpp.png")	{template_6_reversed(0,0)}

spriteset(spriteset_0_series_coach_back_panto_2,          "src/gfx/0_series/coach_back_panto/1x_8bpp.png")	{template_6_standard(0,0)}
spriteset(spriteset_0_series_coach_back_panto_reversed_2, "src/gfx/0_series/coach_back_panto/1x_8bpp.png")	{template_6_reversed(0,0)}

alternative_sprites(spriteset_0_series_coach_back_panto_2,     		
	ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP,     				"src/gfx/0_series/coach_back_panto/2x_8bpp.png")	{template_2x_6_standard(0,0)}

alternative_sprites(spriteset_0_series_coach_back_panto_reversed_2,  
	ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP,     				"src/gfx/0_series/coach_back_panto/2x_8bpp.png")	{template_2x_6_reversed(0,0)}

// Back Cab
spriteset(spriteset_0_series_back_1,              		"src/gfx/0_series/back/1x_8bpp.png")         		{template_6_standard(0,0)}
spriteset(spriteset_0_series_back_reversed_1,				"src/gfx/0_series/back/1x_8bpp.png")         		{template_6_reversed(0,0)}

spriteset(spriteset_0_series_back_2,              		"src/gfx/0_series/back/1x_8bpp.png")         		{template_6_standard(0,0)}
spriteset(spriteset_0_series_back_reversed_2,				"src/gfx/0_series/back/1x_8bpp.png")         		{template_6_reversed(0,0)}

alternative_sprites(spriteset_0_series_back_2,     		
	ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP,     				"src/gfx/0_series/back/2x_8bpp.png")     	 		{template_2x_6_standard(0,0)}
alternative_sprites(spriteset_0_series_back_reversed_2,  
	ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP,     				"src/gfx/0_series/back/2x_8bpp.png")      			{template_2x_6_reversed(0,0)}

/* Purchase Menu */
spriteset (spriteset_0_series_purchase,	        		"src/gfx/0_series/purchase.png")               		{template_purchase(0, 0)}

/* PICK THE ZOOM LEVEL*/

// Front Car zoom choice
switch (FEAT_TRAINS, SELF, spriteset_0_series_front, param_resolution) {
0: spriteset_0_series_front_1;
1: spriteset_0_series_front_2;
}

switch (FEAT_TRAINS, SELF, spriteset_0_series_front_reversed, param_resolution) {
0: spriteset_0_series_front_reversed_1;
1: spriteset_0_series_front_reversed_2;
}

// Coach-front zoom choice
switch (FEAT_TRAINS, SELF, spriteset_0_series_coach_front, param_resolution) {
0: spriteset_0_series_coach_front_1;
1: spriteset_0_series_coach_front_2;
}

switch (FEAT_TRAINS, SELF, spriteset_0_series_coach_front_reversed, param_resolution) {
0: spriteset_0_series_coach_front_reversed_1;
1: spriteset_0_series_coach_front_reversed_2;
}

// Coach-back zoom choice
switch (FEAT_TRAINS, SELF, spriteset_0_series_coach_back, param_resolution) {
0: spriteset_0_series_coach_back_1;
1: spriteset_0_series_coach_back_2;
}

switch (FEAT_TRAINS, SELF, spriteset_0_series_coach_back_reversed, param_resolution) {
0: spriteset_0_series_coach_back_reversed_1;
1: spriteset_0_series_coach_back_reversed_2;
}

// Coach-back-panto zoom choice
switch (FEAT_TRAINS, SELF, spriteset_0_series_coach_back_panto, param_resolution) {
0: spriteset_0_series_coach_back_panto_1;
1: spriteset_0_series_coach_back_panto_2;
}

switch (FEAT_TRAINS, SELF, spriteset_0_series_coach_back_panto_reversed, param_resolution) {
0: spriteset_0_series_coach_back_panto_reversed_1;
1: spriteset_0_series_coach_back_panto_reversed_2;
}

// Back Cab zoom choice
switch (FEAT_TRAINS, SELF, spriteset_0_series_back, param_resolution) {
0: spriteset_0_series_back_1;
1: spriteset_0_series_back_2;
}

switch (FEAT_TRAINS, SELF, spriteset_0_series_back_reversed, param_resolution) {
0: spriteset_0_series_back_reversed_1;
1: spriteset_0_series_back_reversed_2;
}

/* ADD THE LIGHTS */

/* First car of vehicle is a 0_series_front. If it is the first car of the train, display headlights.  */
switch (FEAT_TRAINS, SELF, sw_0_series_front_with_headlights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_0_series_front;               	// Return the base sprite
    1: return spriteset_0_series_front_headlights;    	// Return the overlay
}

/* First car of vehicle is an 0_series_front. If it is the first car of the train, display headlights.  */
switch (FEAT_TRAINS, SELF, sw_0_series_front_position, position_in_consist) {
	0: return sw_0_series_front_with_headlights;
	return spriteset_0_series_front;
}

/* Sprite stack for 0_series_front with rear lights */
switch (FEAT_TRAINS, SELF, sw_0_series_front_with_rear_lights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_0_series_front_reversed;        // Return the base sprite
    1: return spriteset_0_series_front_rearlights;   // Return the overlay
}

/* Last car of vehicle is an 0_series_front. If it is the last car of the train, display rear lights.  */
switch (FEAT_TRAINS, SELF, sw_0_series_front_reversed_position, position_in_consist_from_end) {
	0: return sw_0_series_front_with_rear_lights;
	return spriteset_0_series_front_reversed;
}

/* Sprite stack for 0_series_back with headlights */
switch (FEAT_TRAINS, SELF, sw_0_series_back_with_headlights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_0_series_back_reversed;    		// Return the base sprite
    1: return spriteset_0_series_back_headlights;    	// Return the overlay
}

/* First car of vehicle is an  0_series_back. If it is the first car of the train, display headlights.  */
switch (FEAT_TRAINS, SELF, sw_0_series_back_reversed_position, position_in_consist) {
	0: return sw_0_series_back_with_headlights;
	return spriteset_0_series_back_reversed;
}

/* Sprite stack for 0_series_back with marker lights */
switch (FEAT_TRAINS, SELF, sw_0_series_back_with_marker_lights, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return spriteset_0_series_back;    				// Return the base sprite
    1: return spriteset_0_series_back_rearlights;    	// Return the overlay
}

/* Last car of vehicle is an 0_series_back. If it is the last car of the train, display rear lights.  */
switch (FEAT_TRAINS, SELF, sw_0_series_back_position, position_in_consist_from_end) {
	0: return sw_0_series_back_with_marker_lights;
	return spriteset_0_series_back;
}

/* FORWARDS AND BACKWARDS */

/* FRONT - 2 parts */
/* FRONT CAB check reversed */
switch (FEAT_TRAINS, PARENT, sw_0_series_reverser_front, vehicle_is_reversed) {
	0: sw_0_series_front_position;
	sw_0_series_back_reversed_position;
}

/* BACK OF LEAD CAR check reversed */ 
switch (FEAT_TRAINS, PARENT, sw_0_series_reverser_backofleadcar, vehicle_is_reversed) {
	0: spriteset_0_series_coach_back_panto;													
	spriteset_0_series_coach_front_reversed;													
}

/* REAR - 2 parts */
/* REAR CAB check reversed */
switch (FEAT_TRAINS, PARENT, sw_0_series_reverser_back, vehicle_is_reversed) {
	0: sw_0_series_back_position;
	sw_0_series_front_reversed_position;
}
/* BACK of REAR CAB check reversed */ 
switch (FEAT_TRAINS, PARENT, sw_0_series_reverser_backofrearcar, vehicle_is_reversed) {
	0: spriteset_0_series_coach_front;														
	spriteset_0_series_coach_back_panto_reversed;											
}

/* COACHES 
  Repeating 4 switches to create a;
- Front Coach Car going forwards and a Panto Car going backwards
- Back Coach Car going forwards and a Front Coach Car going backwards
- Front Coach Car going forwards and a Back Car going backwards
- Back Coach Car going forwards and a Front Coach Car going backwards

   Coach 1 check reversed */ 
switch (FEAT_TRAINS, PARENT, sw_0_series_reverser_F_P, vehicle_is_reversed) {
	0: spriteset_0_series_coach_front;
	spriteset_0_series_coach_back_panto_reversed;
}

/* Coach 2 check reversed */ 
switch (FEAT_TRAINS, PARENT, sw_0_series_reverser_B_F, vehicle_is_reversed) {
	0: spriteset_0_series_coach_back;
	spriteset_0_series_coach_front_reversed;
}

/* Coach 3 check reversed */ 
switch (FEAT_TRAINS, PARENT, sw_0_series_reverser_F_B, vehicle_is_reversed) {
	0: spriteset_0_series_coach_front;
	spriteset_0_series_coach_back_reversed;
}

/* Coach 4 check reversed */ 
switch (FEAT_TRAINS, PARENT, sw_0_series_reverser_P_F, vehicle_is_reversed) {
	0: spriteset_0_series_coach_back_panto;
	spriteset_0_series_coach_front_reversed;
}

/* Select the correct part of the train */
switch (FEAT_TRAINS, SELF, sw_0_series_graphics_original, position_in_articulated_veh) {
	0: sw_0_series_reverser_front; 					
	1: sw_0_series_reverser_backofleadcar; 					
	2: sw_0_series_reverser_F_P;					
	3: sw_0_series_reverser_B_F; 					
	4: sw_0_series_reverser_F_B;					
	5: sw_0_series_reverser_P_F;					
	6: sw_0_series_reverser_F_P;					
	7: sw_0_series_reverser_B_F;
	8: sw_0_series_reverser_F_B;
	9: sw_0_series_reverser_P_F;
	10: sw_0_series_reverser_F_P;
	11: sw_0_series_reverser_B_F;
	12: sw_0_series_reverser_F_B;
	13: sw_0_series_reverser_P_F;
	14: sw_0_series_reverser_F_P;
	15: sw_0_series_reverser_B_F;
	16: sw_0_series_reverser_F_B;
	17: sw_0_series_reverser_P_F;
	18: sw_0_series_reverser_F_P;
	19: sw_0_series_reverser_B_F;
	20: sw_0_series_reverser_F_B;
	21: sw_0_series_reverser_P_F;
	22: sw_0_series_reverser_backofrearcar;
	23: sw_0_series_reverser_back;					
}

/* Assemble an 12-car articulated vehicle. */
switch (FEAT_TRAINS, SELF, sw_0_series_articulated_part_original, extra_callback_info1) {
	1 .. 23: return series_0;										// Should be same as range above
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

/* Select the correct part of the train */
switch (FEAT_TRAINS, SELF, sw_0_series_graphics_hikari, position_in_articulated_veh) {
	0: sw_0_series_reverser_front; 					
	1: sw_0_series_reverser_backofleadcar; 					
	2: sw_0_series_reverser_F_P;					
	3: sw_0_series_reverser_B_F; 					
	4: sw_0_series_reverser_F_B;					
	5: sw_0_series_reverser_P_F;					
	6: sw_0_series_reverser_F_P;					
	7: sw_0_series_reverser_B_F;
	8: sw_0_series_reverser_F_B;
	9: sw_0_series_reverser_P_F;
	10: sw_0_series_reverser_F_P;
	11: sw_0_series_reverser_B_F;
	12: sw_0_series_reverser_F_B;
	13: sw_0_series_reverser_P_F;
	14: sw_0_series_reverser_F_P;
	15: sw_0_series_reverser_B_F;
	16: sw_0_series_reverser_F_B;
	17: sw_0_series_reverser_P_F;
	18: sw_0_series_reverser_F_P;
	19: sw_0_series_reverser_B_F;
	20: sw_0_series_reverser_F_B;
	21: sw_0_series_reverser_P_F;
	22: sw_0_series_reverser_F_P;
	23: sw_0_series_reverser_B_F;
	24: sw_0_series_reverser_F_B;
	25: sw_0_series_reverser_P_F;
	26: sw_0_series_reverser_F_P;
	27: sw_0_series_reverser_B_F;
	28: sw_0_series_reverser_F_B;
	29: sw_0_series_reverser_P_F;
	30: sw_0_series_reverser_backofrearcar;
	31: sw_0_series_reverser_back;					
}

/* Assemble an 16-car articulated vehicle. */
switch (FEAT_TRAINS, SELF, sw_0_series_articulated_part_hikari, extra_callback_info1) {
	1 .. 31: return series_0;										// Should be same as range above
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch(FEAT_TRAINS, SELF, sw_0_series_graphics,  cargo_subtype) {
0 : sw_0_series_graphics_original;
1 : sw_0_series_graphics_hikari;
}

switch(FEAT_TRAINS, SELF, sw_0_series_articulated_part,  cargo_subtype) {
0 : sw_0_series_articulated_part_original;
1 : sw_0_series_articulated_part_hikari;
}

switch(FEAT_TRAINS, SELF, sw_0_series_cargo_subtype, cargo_subtype) {
0 : string(LV_0_SERIES_ORIGINAL);
1 : string(LV_0_SERIES_16HIKARI);
}

switch (FEAT_TRAINS, SELF, sw_0_series_visual_effect_and_powered, ((position_in_articulated_veh == 0) && !func_is_reversed()) || ((position_in_articulated_veh_from_end == 0) && func_is_reversed()) ) {
	1: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 0, ENABLE_WAGON_POWER);
	return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

item(FEAT_TRAINS, series_0, 0) {
	property {
		name: 							string(STR_NAME_0_SERIES);
		climates_available:             ALL_CLIMATES;
		introduction_date:              date(1964,10,01);
		model_life:                     22;							// Until 1986
		vehicle_life:                   30;							// Match JapanSet
		reliability_decay:              5;
		refittable_cargo_classes:       bitmask(CC_PASSENGERS);
		refit_cost:                     0;
		default_cargo_type:             PASS;
		cargo_capacity:                 50;							// To approx JapanSet train on the same length
		loading_speed:                  50;							
		cargo_age_period:               278; 						// 1.5 times normal for this expensive long-distance train
		sprite_id:                      SPRITE_ID_NEW_TRAIN; 		// required to enable new graphics
		speed:                          211 km/h;					// Operational Top Speed
		misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
		track_type:                     HIGH_SPEED;
		ai_special_flag:                AI_FLAG_PASSENGER;
		running_cost_base:              RUNNING_COST_STEAM;
		engine_class:                   ENGINE_CLASS_ELECTRIC;
		power:                          13500 kW;
		weight:                         1098 tons;
		tractive_effort_coefficient:    0.3;
		length:                         6;
	}
	graphics {
		default:                        sw_0_series_graphics;
		purchase:                       spriteset_0_series_purchase;
		articulated_part:               sw_0_series_articulated_part;
		//colour_mapping:                 sw_cc_blue_white;
		cargo_subtype_text:             sw_0_series_cargo_subtype;
		cost_factor:                    257;
		running_cost_factor:            func_running_costs(300);
		purchase_running_cost_factor:   300;
		visual_effect_and_powered:      sw_0_series_visual_effect_and_powered;
		additional_text:                string(STR_ROLE_EXPRESS_PASS);
	}
}

/* End 0 series */
