/* Begin 800 Series */

/* GRAPHICS */

    // Purchase Menu
    spriteset (ss_800_series_buy_12,	      		"src/trains/dummy/purchase.png") {template_purchase(0, 0)}
	spriteset (ss_800_series_buy_10,	      		"src/trains/dummy/purchase.png") {template_purchase(0, 0)}
    alternative_sprites(ss_800_series_buy_12,       ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/800_series/12/purchase.png") {template_purchase(0, 0)}
	alternative_sprites(ss_800_series_buy_10,       ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/800_series/12/purchase.png") {template_purchase(0, 0)}

		switch (FEAT_TRAINS, SELF, ss_800_series_buy, param_spritelength) {
			1: ss_800_series_buy_10;				// Length 10
			ss_800_series_buy_12;					// Length 12
		}

    // "CAB"
    spriteset(ss_800_series_cab_12,             	"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_800_series_cab_rev_12,            	"src/trains/dummy/empty.png") {template_dummy(0,0)}
	spriteset(ss_800_series_cab_10,             	"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_800_series_cab_rev_10,            	"src/trains/dummy/empty.png") {template_dummy(0,0)}

    alternative_sprites(ss_800_series_cab_12,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/800_series/12/800_series.png") {template_12_standard(1,1)}
    alternative_sprites(ss_800_series_cab_rev_12,   ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/800_series/12/800_series.png") {template_12_reversed(1,41)}
	alternative_sprites(ss_800_series_cab_10,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/800_series/10/800_series.png") {template_10_standard(1,1)}
    alternative_sprites(ss_800_series_cab_rev_10,   ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/800_series/10/800_series.png") {template_10_reversed(1,41)}	

		switch (FEAT_TRAINS, SELF, ss_800_series_cab, param_spritelength) {
			1: ss_800_series_cab_10;				// Length 10
			ss_800_series_cab_12;					// Length 12
		}		

		switch (FEAT_TRAINS, SELF, ss_800_series_cab_rev, param_spritelength) {
			1: ss_800_series_cab_rev_10;			// Length 10
			ss_800_series_cab_rev_12;				// Length 12
		}

    // COACH
    spriteset(ss_800_series_coach_12,             	"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_800_series_coach_rev_12,    		"src/trains/dummy/empty.png") {template_dummy(0,0)}
	spriteset(ss_800_series_coach_10,             	"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_800_series_coach_rev_10,    		"src/trains/dummy/empty.png") {template_dummy(0,0)}

    alternative_sprites(ss_800_series_coach_12,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/800_series/12/800_series.png") {template_12_standard(1,121)}
    alternative_sprites(ss_800_series_coach_rev_12, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/800_series/12/800_series.png") {template_12_reversed(1,121)}
	alternative_sprites(ss_800_series_coach_10,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/800_series/10/800_series.png") {template_10_standard(1,121)}
    alternative_sprites(ss_800_series_coach_rev_10, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/800_series/10/800_series.png") {template_10_reversed(1,121)}

		switch (FEAT_TRAINS, SELF, ss_800_series_coach, param_spritelength) {
			1: ss_800_series_coach_10;				// Length 10
			ss_800_series_coach_12;					// Length 12
		}		

		switch (FEAT_TRAINS, SELF, ss_800_series_coach_rev, param_spritelength) {
			1: ss_800_series_coach_rev_10;			// Length 10
			ss_800_series_coach_rev_12;				// Length 12
		}

    // COACH PANTO
    spriteset(ss_800_series_panto_12,          		"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_800_series_panto_rev_12, 		    "src/trains/dummy/empty.png") {template_dummy(0,0)}
	spriteset(ss_800_series_panto_10,          		"src/trains/dummy/empty.png") {template_dummy(0,0)}
    spriteset(ss_800_series_panto_rev_10, 		    "src/trains/dummy/empty.png") {template_dummy(0,0)}

    alternative_sprites(ss_800_series_panto_12,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/800_series/12/800_series.png") {template_12_standard(1,81)}
    alternative_sprites(ss_800_series_panto_rev_12, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/800_series/12/800_series.png") {template_12_reversed(1,81)}	
	alternative_sprites(ss_800_series_panto_10,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/800_series/10/800_series.png") {template_10_standard(1,81)}
    alternative_sprites(ss_800_series_panto_rev_10, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trains/800_series/10/800_series.png") {template_10_reversed(1,81)}	

		switch (FEAT_TRAINS, SELF, ss_800_series_panto, param_spritelength) {
			1: ss_800_series_panto_10;				// Length 10
			ss_800_series_panto_12;					// Length 12
		}		

		switch (FEAT_TRAINS, SELF, ss_800_series_panto_rev, param_spritelength) {
			1: ss_800_series_panto_rev_10;			// Length 10
			ss_800_series_panto_rev_12;				// Length 12
		}

/* FORMATIONS */

switch (FEAT_TRAINS, SELF, sw_800_series_backwards_long, position_in_consist_from_end % 48) {				
		13: ss_800_series_panto_rev;
		28: ss_800_series_panto_rev;
		ss_800_series_coach_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_800_series_backwards_short, position_in_consist_from_end % 18) {				
		4:  ss_800_series_panto_rev;
		13: ss_800_series_panto_rev;
		ss_800_series_coach_rev;				
	}

// Forwards

switch (FEAT_TRAINS, SELF, sw_800_series_forwards_panto_in_middle, (position_in_consist == position_in_consist_from_end ) || (position_in_consist - 3 == position_in_consist_from_end)) {				
		0:  ss_800_series_coach;
		ss_800_series_panto;				
	}

switch (FEAT_TRAINS, SELF, sw_800_series_forwards_2nd_and_penultimate, position_in_consist == 4 || position_in_consist_from_end  == 4) {				
		0:  ss_800_series_coach;
		ss_800_series_panto;				
	}

switch (FEAT_TRAINS, SELF, sw_800_series_forwards, num_vehs_in_consist ) {				
		0..17:  sw_800_series_forwards_panto_in_middle;
        sw_800_series_forwards_2nd_and_penultimate;				
	}

switch (FEAT_TRAINS, SELF, sw_800_series_backwards, num_vehs_in_consist <= 18) {				
		0:  sw_800_series_backwards_long;
		sw_800_series_backwards_short;				
	}

switch (FEAT_TRAINS, PARENT, sw_800_series_midcars, vehicle_is_reversed) {				
		0:  sw_800_series_forwards;
		sw_800_series_backwards;				
	}

switch (FEAT_TRAINS, SELF, sw_800_series_back, position_in_consist_from_end) {				
		1:  ss_800_series_cab_rev;
		sw_800_series_midcars;				
	}

switch (FEAT_TRAINS, SELF, sw_800_series_front, position_in_consist) {				
		1:  ss_800_series_cab;
		sw_800_series_back;				
	}

switch (FEAT_TRAINS, SELF, sw_800_series_graphics, position_in_consist % 3) {				
		1:  sw_800_series_front; 				
		dummy_sprites;
	}

switch (FEAT_TRAINS, SELF, sw_800_series_articulated_part, extra_callback_info1) {
		1 .. 2: return series_800;									
		return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
	}

/*---Coupling Switch---*/
switch(FEAT_TRAINS, SELF, sw_800_series_attach_vehid, [vehicle_type_id == mu_car_full]) {
 0: return string(ONLY_ADD_MUCAR);
}

/*---Length Check---*/
switch (FEAT_TRAINS, SELF, sw_800_series_stop_start, num_vehs_in_consist < 12) {				
		1:  return string(STR_LENGTH_ERROR); 				
		return CB_RESULT_NO_TEXT; 
	}

item(FEAT_TRAINS, series_800) {
	property {
		/* Menu */
		name: 							string(STR_NAME_800_SERIES);
		sprite_id:                      SPRITE_ID_NEW_TRAIN; 		// required to enable new graphics
		misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
		running_cost_base:              RUNNING_COST_ELECTRIC;
		engine_class:                   ENGINE_CLASS_ELECTRIC;
		/* Availability */
		climates_available:             ALL_CLIMATES;
		introduction_date:              date(2004,03,13);
		model_life:                     VEHICLE_NEVER_EXPIRES;		//
		vehicle_life:                   30;							// 
		track_type:                     SHINKANSEN;
		/* Cargo */
		reliability_decay:              5;
		refittable_cargo_classes:       bitmask(CC_PASSENGERS);
		refit_cost:                     0;
		default_cargo_type:             PASS;
		ai_special_flag:                AI_FLAG_PASSENGER;
		cargo_capacity:                 1;							// 
		loading_speed:                  50;							
		cargo_age_period:               278; 						// 1.5 times normal for this expensive long-distance train
		/* Stats */
		speed:                          261 km/h;				
		power:                          9300 hp;					// 1550 hp (per JPS) x 6 cars					
		weight:                         44 * 6 tons;				// 44t (per JPS) x 6 cars					
		tractive_effort_coefficient:    0.3;
		air_drag_coefficient:			0;
	}
	graphics {
		/* Menu */
		additional_text:                string(STR_DESC_800_SERIES);
		//cargo_subtype_text:             sw_800_series_subtype_text;
		can_attach_wagon:				sw_800_series_attach_vehid;
		start_stop:						sw_800_series_stop_start;
		purchase_weight:                44;
        purchase_power:                 375 * 4;
		/* Graphics */
		default:                        sw_800_series_graphics;
		purchase:                       ss_800_series_buy;
		articulated_part:               sw_800_series_articulated_part;
		visual_effect_and_powered:      sw_visual_effect_and_powered;
		/* Stats */
		length:                         sw_param_length;
        power:                          NumberCarsInConsist() * 375 * 4;	// (285 kW)
        weight:                         NumberCarsInConsist() * 44 ;
		cargo_capacity:                 22 * CapacityFactor();															
		loading_speed:					22 * CapacityFactor() / param_loading_2D;					// Capacity divided by door loading factor
		cargo_age_period:				param_decay_express;
		cost_factor:                    121;
		running_cost_factor:            (290 + 120 * NumberCarsInConsist() ) * RunningCostFactor();
		purchase_running_cost_factor:   290 + 120 * NumberCarsInConsist();
	}
	/*Multiple Unit Car*/
	
      livery_override(mu_car_full){
        default:                        sw_800_series_graphics;
        cargo_capacity:                 22 * CapacityFactor();
        cargo_age_period:				param_decay_express;
        loading_speed:                  22 * CapacityFactor() / param_loading_2D;
		articulated_part:				sw_mucar_full_articulated_part;
		length:							sw_param_length;
		//power:						// Apply power in graphics block to calculate power based on number of motorised cars correctly
		//weight:						// Apply weight to graphics block to calculate Max T.E. correctly
        //running_cost_factor:            sw_800_series_running_costs;
      }
}

/* End 800 series */
