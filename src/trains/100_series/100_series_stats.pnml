/* Begin 100 Series STATS */

/* LENGTH*/

    // Make the Middle long sprite length 8 and the dummy sprites length 2
    switch (FEAT_TRAINS, SELF, sw_100_series_length_standard, position_in_consist%3) {
        1: return 8;
        return 2;
    }

    // Make a 12 car set by adjusting sprite size to 1-1-8-2
    switch(FEAT_TRAINS, SELF, sw_100_series_shorten_12_car_vehicle, position_in_consist%4) {
        2: return 8;
        3: return 2;
        return 1;
    }

    // Make a 8 car set by adjusting sprite size to 1-1-7-1-1-1
    switch(FEAT_TRAINS, SELF, sw_100_series_shorten_8_car_vehicle, position_in_consist%6) {
        2: return 7;
        return 1;
    }

    switch(FEAT_TRAINS, SELF, sw_100_series_length_year_dep, cargo_subtype) {
        0..1999: sw_100_series_length_standard;     // 16-car
        sw_100_series_shorten_8_car_vehicle;        // 8-car
    }

    switch(FEAT_TRAINS, SELF, sw_100_series_length, cargo_subtype) {
        0: sw_100_series_length_year_dep;           // Year Dependent
        2: sw_100_series_shorten_12_car_vehicle;    // 12-car
        4: sw_100_series_shorten_8_car_vehicle;     // 8-car
        sw_100_series_length_standard;              // All other 16-length sets
    }

/* CARGO CAPACITY 

    16-car JPS  1248/1560/1872/2184
    12-car JPS  936/1170/1404/1638
    8-car JPS   624/780/936/1092

        16-car(X)   16-car(V)   12-car  8-car
    IRL 1277        1285        1031    638* 
    RW  1296        1296        1056    624        
    LS  1920        1920        1440    960     
    DC  2592        2592        1968    1296         

    * made up since no length 8 sets

*/

    // 16 car
    switch(FEAT_TRAINS, SELF, sw_100_series_param_cargo_capacity_16car, param_basecapacity) {
        0: return 27;       // 1x Real World Capacity
        2: return 54;       // 2x Double Real World Capacity
        return 40;          // 1.5x Long Sprite Adjusted Capacity 
    }

    // 12 car
    switch(FEAT_TRAINS, SELF, sw_100_series_param_cargo_capacity_12car, param_basecapacity) {
        0: return 22;       // 1x Real World Capacity
        2: return 41;       // 2x Double Real World Capacity
        return 30;          // 1.5x Long Sprite Adjusted Capacity 
    }

    // 8 car
    switch(FEAT_TRAINS, SELF, sw_100_series_param_cargo_capacity_8car, param_basecapacity) {
        0: return 13;       // 1x Real World Capacity
        2: return 27;       // 2x Double Real World Capacity
        return 20;          // 1.5x Long Sprite Adjusted Capacity 
    }

    // Scaling factor for each length of consist
    switch(FEAT_TRAINS, SELF, sw_100_series_cargo_capacity, cargo_subtype) {
        2: sw_100_series_param_cargo_capacity_12car;
        4: sw_100_series_param_cargo_capacity_8car;
        sw_100_series_param_cargo_capacity_16car;  
    }

/* RUNNING COSTS */
    switch(FEAT_TRAINS, SELF, sw_100_series_running_costs, cargo_subtype) {
        2: return 113;
        4: return 75;
        return 300;
    }

/* WEIGHT */
    switch(FEAT_TRAINS, SELF, sw_100_series_weight, cargo_subtype) {
        2: return 53*12;        // 53t (per JPS) x 12 cars
        4: return 53*8;         // 53t (per JPS) x 08 cars
        return 53*16;           // 53t (per JPS) x 16 cars
    }

/* POWER */
    switch(FEAT_TRAINS, SELF, sw_100_series_power, cargo_subtype) {
        // 230 kW a motor (per HAGIWARA doc), 4 motors per car
        2: return 230*4*10*4/3;         // 230 kW x 4 x 10 motorised cars converted to hp
        4: return 230*4*8*4/3;          // 230 kW x 4 x 08 motorised cars converted to hp
        return 230*4*12*4/3;            // 230 kW x 4 x 12 motorised cars converted to hp
    }

/* SPEED */
    switch(FEAT_TRAINS, SELF, sw_100_series_speed, cargo_subtype) {
        0..3: return 220;   // Original speed
        return 230;         // Kodama speed
    }
