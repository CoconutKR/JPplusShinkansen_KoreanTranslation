/* Begin 100 Series */

#include "src/trains/100_series/100_series_subtypes.pnml"
#include "src/trains/100_series/100_series_gfx.pnml"
#include "src/trains/100_series/100_series_liveries.pnml"

// Midcars going backwards
switch (FEAT_TRAINS, SELF, sw_100_series_backwards_v, position_in_consist_from_end % 48) {				
		4:  ss_100_series_panto_rev;
		10: ss_100_series_panto_rev;
		16: ss_100_series_panto_rev;
		19: ss_100_series_2deck_rev;
		22: ss_100_series_2deck_rev;
		25: ss_100_series_2dec2_rev;
		28: ss_100_series_2deck_rev;
		34: ss_100_series_panto_rev;
		40: ss_100_series_panto_rev;
		ss_100_series_coach_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_100_series_backwards_others, position_in_consist_from_end % 6) {				
		4:  ss_100_series_panto_rev;
		ss_100_series_coach_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_100_series_backwards_year_dep, build_year) {				
		1988..1999:  sw_100_series_backwards_v;
		sw_100_series_backwards_others;				
	}

switch (FEAT_TRAINS, SELF, sw_100_series_backwards, cargo_subtype) {				
		0:  sw_100_series_backwards_year_dep;
		2:  sw_100_series_backwards_v;
		sw_100_series_backwards_others;				
	}

// Midcars going forwards
switch (FEAT_TRAINS, SELF, sw_100_series_forwards_v, position_in_consist % 48) {				
		4:  ss_100_series_panto;
		10: ss_100_series_panto;
		16: ss_100_series_panto;
		19: ss_100_series_2deck;
		22: ss_100_series_2deck;
		25: ss_100_series_2dec2;
		28: ss_100_series_2deck;
		34: ss_100_series_panto;
		40: ss_100_series_panto;
		ss_100_series_coach;				
	}

switch (FEAT_TRAINS, SELF, sw_100_series_forwards_others, position_in_consist % 6) {				
		4:  ss_100_series_panto;
		ss_100_series_coach;				
	}

switch (FEAT_TRAINS, SELF, sw_100_series_forwards_year_dep, build_year) {				
		1988..1999:  sw_100_series_forwards_v;
		sw_100_series_forwards_others;				
	}

switch (FEAT_TRAINS, SELF, sw_100_series_forwards, cargo_subtype) {				
		0:  sw_100_series_forwards_year_dep;
		2:  sw_100_series_forwards_v;
		sw_100_series_forwards_others;				
	}

// Is vehicle reversed?
switch (FEAT_TRAINS, PARENT, sw_100_series_midcars, vehicle_is_reversed) {				
		0:  sw_100_series_forwards;
		sw_100_series_backwards;				
	}

// Back Car Pantocheck
switch (FEAT_TRAINS, PARENT, sw_100_series_back_evenreversecheck, vehicle_is_reversed) {				
		0:  ss_100_series_cabwp_rev;
		ss_100_series_cab_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_100_series_back_oddevencheck, num_vehs_in_consist % 2) {				
		0:  sw_100_series_back_evenreversecheck;
		ss_100_series_cab_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_100_series_back, position_in_consist_from_end) {				
		1:  sw_100_series_back_oddevencheck;
		sw_100_series_midcars;				
	}

// Front Car Pantocheck
switch (FEAT_TRAINS, SELF, sw_100_series_front_oddevencheck, num_vehs_in_consist % 2) {				
		1:  ss_100_series_cab;
		ss_100_series_cabwp;				
	}

switch (FEAT_TRAINS, PARENT, sw_100_series_front_reversecheck, vehicle_is_reversed) {				
		0:  ss_100_series_cab;
		sw_100_series_front_oddevencheck;				
	}

switch (FEAT_TRAINS, SELF, sw_100_series_front, position_in_consist) {				
		1:  sw_100_series_front_reversecheck;
		sw_100_series_back;				
	}

// GRAPHICS
switch (FEAT_TRAINS, SELF, sw_100_series_graphics, position_in_consist % 3) {				
		1:  sw_100_series_front; 				
		dummy_sprites;
	}

switch (FEAT_TRAINS, SELF, sw_100_series_articulated_part, extra_callback_info1) {
		1 .. 2: return series_100;									
		return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
	}

/*---Coupling Switch---*/
switch(FEAT_TRAINS, SELF, sw_100_series_attach_vehid, [vehicle_type_id == mu_car_full]) {
 0: return string(ONLY_ADD_MUCAR);
}

/*---Length Check---*/
switch (FEAT_TRAINS, SELF, sw_100_series_stop_start, num_vehs_in_consist < 12) {				
		1:  return string(STR_LENGTH_ERROR); 				
		return CB_RESULT_NO_TEXT; 
	}

/* SPEED */
    switch(FEAT_TRAINS, SELF, sw_100_series_speed, cargo_subtype) {
        0..2: return 220;   // Original speed
        return 230;         // Kodama speed
    }

item(FEAT_TRAINS, series_100) {
	property {
		/* Menu */
        name: 							string(STR_NAME_100_SERIES);
		sprite_id:                      SPRITE_ID_NEW_TRAIN; 		// required to enable new graphics
        misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
		running_cost_base:              RUNNING_COST_ELECTRIC;
		engine_class:                   ENGINE_CLASS_ELECTRIC;
        /* Availability */
        climates_available:             ALL_CLIMATES;
		introduction_date:              date(1985,10,01);           // Date of launch
		model_life:                     26;		                    // 
		vehicle_life:                   30;							// 
		track_type:                     SHINKANSEN;
        /* Cargo */
        reliability_decay:              5;
		refittable_cargo_classes:       bitmask(CC_PASSENGERS);
		refit_cost:                     0;
		default_cargo_type:             PASS;
		ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;							// Changed by callback
		loading_speed:                  50;							
		cargo_age_period:               278; 						// 1.5 times normal for this expensive long-distance train		
		/* Stats */
        speed:                          211 km/h;                   // Changed by callback
		power:                          8050 kW;					// Changed by callback
		weight:                         1464 ton;                   // Changed by callback					
		tractive_effort_coefficient:    0.3;
		air_drag_coefficient:			0;
	}
	graphics {
		/* Menu */
        additional_text:                string(STR_DESC_100_SERIES);
        cargo_subtype_text:             sw_100_series_year_dep_subtype_text;
		can_attach_wagon:				sw_100_series_attach_vehid;
		start_stop:						sw_100_series_stop_start;
        /* Graphics */
        default:                        sw_100_series_graphics;
		purchase:                       sw_100_series_year_dep_purchase;
		articulated_part:               sw_100_series_articulated_part;
		visual_effect_and_powered:      sw_visual_effect_and_powered;
        /* Stats */
        length:                         sw_length_2_8_2;
        weight:                         NumberCarsInConsist() * 53;
        power:                          max (NumberCarsInConsist() - 2 ,0) * 308 * 4;	// 230 kW
        speed:							sw_100_series_speed;
		cargo_capacity:                 27 * CapacityFactor();															
		loading_speed:					27 * CapacityFactor() / param_loading_2D;					// Capacity divided by door loading factor
		cargo_age_period:				param_decay_express;
		cost_factor:                    132;
		running_cost_factor:            (300 + 123 * NumberCarsInConsist() ) * RunningCostFactor();
		purchase_running_cost_factor:   300 + 123 * NumberCarsInConsist();
		
	}
	/*Multiple Unit Car*/
	
      livery_override(mu_car_full){
        default:                        sw_100_series_graphics;
        cargo_capacity:                 27 * CapacityFactor();	
        cargo_age_period:				param_decay_express;
        loading_speed:					27 * CapacityFactor() / param_loading_2D;
		articulated_part:				sw_mucar_full_articulated_part;
		length:							sw_length_2_8_2;
		//power:						// Apply power in graphics block to calculate power based on number of motorised cars correctly
		//weight:						// Apply weight to graphics block to calculate Max T.E. correctly
        //running_cost_factor:            sw_100_series_running_costs;
      }
}

/* End 100 Series */
