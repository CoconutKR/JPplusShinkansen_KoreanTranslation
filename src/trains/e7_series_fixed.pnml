/* Begin e7 Series */

/* CARGO SUBTYPE MENU*/

    // E7 and W7 Series
    switch(FEAT_TRAINS, SELF, sw_e7_series_subtype_text, cargo_subtype) {
        0: return string(LV_E7_SERIES_E7);
	    1: return string(LV_E7_SERIES_W7);
        return CB_RESULT_NO_TEXT;
        }

/* SPRITES */

    // Purchase Menu
    spriteset (ss_e7_series_buy,	        	    "src/gfx/dummy/purchase.png")           {template_purchase(0, 0)}
    alternative_sprites(ss_e7_series_buy,           ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,     "src/gfx/e7_series/purchase.png") {template_purchase(0, 0)}

    // "CAB"
    spriteset(ss_e7_series_cab,             		"src/gfx/dummy/empty.png")     			{template_dummy(0,0)}
    spriteset(ss_e7_series_cab_rev,             	"src/gfx/dummy/empty.png")     			{template_dummy(0,0)}
    alternative_sprites(ss_e7_series_cab,     	    ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,     "src/gfx/e7_series/e7_series.png") {template_12_standard(1,1)}
    alternative_sprites(ss_e7_series_cab_rev,       ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,     "src/gfx/e7_series/e7_series.png") {template_12_reversed(1,41)}		

    // COACH
    spriteset(ss_e7_series_coach,             		"src/gfx/dummy/empty.png")     			{template_dummy(0,0)}
    spriteset(ss_e7_series_coach_rev,    		    "src/gfx/dummy/empty.png")     			{template_dummy(0,0)}
    alternative_sprites(ss_e7_series_coach,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,     "src/gfx/e7_series/e7_series.png") {template_12_standard(1,121)}
    alternative_sprites(ss_e7_series_coach_rev,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,     "src/gfx/e7_series/e7_series.png") {template_12_reversed(1,121)}

    // COACH PANTO
    spriteset(ss_e7_series_panto,          		    "src/gfx/dummy/empty.png")     			{template_dummy(0,0)}
    spriteset(ss_e7_series_panto_rev, 		        "src/gfx/dummy/empty.png")     			{template_dummy(0,0)}
    alternative_sprites(ss_e7_series_panto,     	ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,     "src/gfx/e7_series/e7_series.png") {template_12_standard(1,81)}
    alternative_sprites(ss_e7_series_panto_rev,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,     "src/gfx/e7_series/e7_series.png") {template_12_reversed(1,81)}	

/* REVERSERS */

switch (FEAT_TRAINS, PARENT, sw_e7_series_reverser_C_P, vehicle_is_reversed) {
	0: ss_e7_series_coach;
	ss_e7_series_panto_rev;
}

switch (FEAT_TRAINS, PARENT, sw_e7_series_reverser_C_C, vehicle_is_reversed) {
	0: ss_e7_series_coach;
	ss_e7_series_coach_rev;
}

switch (FEAT_TRAINS, PARENT, sw_e7_series_reverser_P_C, vehicle_is_reversed) {
	0: ss_e7_series_panto;
	ss_e7_series_coach_rev;
}

/* LENGTH*/

    // Make the Middle long sprite length 8 and the dummy sprites length 2
    switch (FEAT_TRAINS, SELF, sw_e7_series_length, position_in_consist%3) {
        1: return 8;
        return 2;
    }

/* PUT TOGETHER THE EMU*/

    // 10 Car Set
    switch (FEAT_TRAINS, SELF, sw_e7_series_graphics, position_in_articulated_veh) {					
	    1:  ss_e7_series_cab; 	            // Car 1	Car 12								
	    4:  sw_e7_series_reverser_C_C;	    // Car 2	Car 11	    						
	    7:  sw_e7_series_reverser_P_C;      // Car 3    Car 10
	    10: sw_e7_series_reverser_C_C;      // Car 4    Car 9
        13: sw_e7_series_reverser_C_C;      // Car 5    Car 8
        16: sw_e7_series_reverser_C_P;      // Car 6    Car 7
        19: sw_e7_series_reverser_P_C;      // Car 7    Car 6
	    22: sw_e7_series_reverser_C_C;      // Car 8    Car 5
        25: sw_e7_series_reverser_C_C;      // Car 9    Car 4
        28: sw_e7_series_reverser_C_P;      // Car 10   Car 3
        31: sw_e7_series_reverser_C_C;      // Car 11   Car 2
        34: ss_e7_series_cab_rev;           // Car 12   Car 1
	    dummy_sprites;
    }

/* Assemble an articulated vehicle. */
switch (FEAT_TRAINS, SELF, sw_e7_series_articulated_part, extra_callback_info1) {
	1 .. 35: return series_e7;										// Should be same as range above
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, sw_e7_series_visual_effect_and_powered, ((position_in_articulated_veh == 0) && !func_is_reversed()) || ((position_in_articulated_veh_from_end == 0) && func_is_reversed()) ) {
	1: return visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 0, ENABLE_WAGON_POWER);
	return visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

/* CARGO CAPACITY */

    // Passenger Capacities for each parameter level
    switch(FEAT_TRAINS, SELF, sw_e7_series_param_cargo_capacity, param_basecapacity) {
        // Assuming 65 capacity per car if it was 0.5 tile (same as JapanSet)
        0: return 52;       // Low Capacity
        2: return 78;       // High Capacity
        3: return 91;       // Very High Capacity
        return 65;          // Normal Capacity 
    }

    // Scaling factor for each length of consist
    switch(FEAT_TRAINS, SELF, sw_e7_series_cargo_capacity_factor, cargo_subtype) {
        1: return 5;     // Length 10 (7.5 tiles)
        return 4;        // Length 8 (6 tiles)
    }

    switch(FEAT_TRAINS, SELF, sw_e7_series_cargo_capacity, (sw_e7_series_param_cargo_capacity() * sw_e7_series_cargo_capacity_factor() ) / 10 ){return;}

/* WEIGHT */
    switch(FEAT_TRAINS, SELF, sw_e7_series_weight, cargo_subtype) {
        1: return 690;      // Weight of a 7.5 tile long JapanSet E2 Series
        return 552;         // Weight of a 6.0 tile long JapanSet E2 Series
    }

/* POWER */
    switch(FEAT_TRAINS, SELF, sw_e7_series_power, cargo_subtype) {
        1: return 18250; 
        return 14600;    
    }

item(FEAT_TRAINS, series_e7) {
	property {
		name: 							string(STR_NAME_E7_SERIES);
		climates_available:             ALL_CLIMATES;
		introduction_date:              date(2014,03,15);
		model_life:                     VEHICLE_NEVER_EXPIRES;		//
		vehicle_life:                   30;							// 
		reliability_decay:              5;
		refittable_cargo_classes:       bitmask(CC_PASSENGERS);
		refit_cost:                     0;
		default_cargo_type:             PASS;
		cargo_capacity:                 1;							// 
		loading_speed:                  50;							
		cargo_age_period:               278; 						// 1.5 times normal for this expensive long-distance train
		sprite_id:                      SPRITE_ID_NEW_TRAIN; 		// required to enable new graphics
		speed:                          275 km/h;				// Operational Top Speed
		misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
		track_type:                     SHINKANSEN;
		ai_special_flag:                AI_FLAG_PASSENGER;
		running_cost_base:              RUNNING_COST_ELECTRIC;
		engine_class:                   ENGINE_CLASS_ELECTRIC;
		power:                          8050 kW;					
		weight:                         648 tons;					
		tractive_effort_coefficient:    0.3;

	}
	graphics {
		default:                        sw_e7_series_graphics;
		purchase:                       ss_e7_series_buy;
		articulated_part:               sw_e7_series_articulated_part;
		length:                         sw_e7_series_length;
        power:                          sw_e7_series_power;
        weight:                         sw_e7_series_weight;
		cargo_capacity:                 sw_e7_series_cargo_capacity;
		cargo_subtype_text:             sw_e7_series_subtype_text;
		cost_factor:                    257;
		running_cost_factor:            func_running_costs(300);
		purchase_running_cost_factor:   300;
		visual_effect_and_powered:      sw_e7_series_visual_effect_and_powered;
		additional_text:                string(STR_DESC_E7_SERIES);
	}
}

/* End e7 series */
